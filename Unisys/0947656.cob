000000$SET PRTLIBC85
000000
000000 IDENTIFICATION DIVISION.
000000
000000 PROGRAM-ID.     S0947-OBJ-656.
000000
000000 AUTHOR.         ANALISE.......  SIMONE MENEZES.
000000                 PROGRAMACAO...  MARCELO DOLABELLA.
000000
000000 INSTALLATION.   UNISYS - BELO HORIZONTE - M.G.
000000
000000 DATE-WRITTEN.   JULHO/2019.
000000
000000 DATE-COMPILED.
000000
000000*----------------------------------------------------------------*
000000*                       RELATORIO MAC/MEX                        *
000000*----------------------------------------------------------------*
000000
000000 ENVIRONMENT DIVISION.
000000
000000 CONFIGURATION SECTION.
000000
000000 SOURCE-COMPUTER.    A15.
000000 OBJECT-COMPUTER.    A15.
000000 SPECIAL-NAMES.      DECIMAL-POINT   IS  COMMA.
000000
000000
000000*INPUT-OUTPUT SECTION.
000000
000000*FILE-CONTROL.
000000
000000 DATA DIVISION.
000000
000000*FILE SECTION.
000000
000000 DATA-BASE SECTION.
000000
000000 DB  BDSEGURANCA OF  BDDATAMEC.
000000
000000 WORKING-STORAGE SECTION.
000000
000000 01 SV-AREA-REL.
000000    COPY "(D0947F05)S0947/LIB/SRV/V094765601 ON PROJETO01".
000000
000000 01  CLI-JANELA-SEG-WS           PIC 9(04).
000000 01  CLI-JANELA-FUN              PIC 9(04).
000000
000000 77  RESULTADO-77                PIC S9(11)  BINARY EXTENDED.
000000 77  PROGRAMA-DESIGNATOR-77      REAL.
000000 77  AGENDA-INPUT-77             REAL.
000000 77  AGENDA-DESTINO-77           REAL.
000000 77  WINDOW-DESIGNATOR-77        REAL.
000000 77  ESTACAO-REAL-77             REAL.
000000 77  STATION-DESIG-77            PIC S9(11)  BINARY.
000000 77  STATION-DEVICE-77           PIC S9(11)  BINARY.
000000 77  STATION-LSN-77              PIC S9(11)  BINARY.
000000 77  STATION-SEC-77              PIC S9(11)  BINARY.
000000 77  IND-SEC-GLOBAL-77           PIC 9(02).
000000 77  CODIGO-INFORMACAO-77        PIC S9(11) BINARY EXTENDED.
000000 77  ENVIA-TELA-77               PIC S9(11)  BINARY EXTENDED.
000000
000000 01 VARIAVEIS.
000000   02 TAB-SISTEMA-FLAG.
000000      03  TAB-SISTEMA-OK              PIC 9(1) OCCURS 10000.
000000   02  FLAG-MAC-SEC              PIC X(1).
000000   02  FLAG-MEX                  PIC X(1).
000000   02  TIPO-CODIGO-SEC-WS        PIC X(3).
000000   02  IND-FUNC-WS               PIC X(1).
000000   02  UNO-CODIGO-SEC-WS         PIC 9(04).
000000   02  FCO-CODIGO-SEC-WS         PIC 9(04).
000000   02  TAMANHO-AREA-WS           PIC 9(05).
000000   02  SIS-CODIGO-WS             PIC 9(04).
000000   02  GOP-CODIGO-WS             PIC 9(04).
000000   02  PER-CODIGO-WS             PIC 9(04).
000000   02  UNO-CODIGO-WS             PIC 9(04).
000000   02  FCO-CODIGO-WS             PIC 9(04).
000000   02  RESULTADO-WS                PIC S9(11)  BINARY.
000000   02  MENSAGEM-WS                 PIC X(160).
000000   02  AGENDA-NOME-WS              PIC X(17).
000000   02  TELA-MENSAGEM-WS.
000000     03  FILLER                  PIC 9(12)   COMP    VALUE
000000         @27E7277F4FF6@.
000000     03  LINHA-MENSAGEM-WS       PIC X(169).
000000     03  FILLER                  PIC 9(06)   COMP    VALUE
000000         @27E603@.
000000
000000   02  IDENTIFICADOR-GETINFO.
000000     03  TAMANHO-GETINFO         PIC 9(04).
000000     03  CONTEUDO-GETINFO        PIC X(80).
000000     03  CONTEUDO-GETINFO-R1  REDEFINES  CONTEUDO-GETINFO.
000000         05  IND-SECULO-GETINFO  PIC 9(02).
000000         05  RESTO-GETINFO       PIC X(78).
000000
000000   02 WS-CONT-GRUPOS                  PIC 9(03).
000000   02 WS-CONT-PERFIL                  PIC 9(03).
000000   02 WS-UNO-CODIGO                   PIC X(07).
000000   02 VERSAO-SEG-WS                   PIC 9(01).
000000   02 COD-FUNCAO-AUX                  PIC 9(04).
000000* CODIGO USUARIO
000000   02  USU-CODIGO-WS                   PIC X(07).
000000   02  USU-CODIGO-WS-RE REDEFINES USU-CODIGO-WS.
000000      03  USU-LETRA-WS                PIC X(01).
000000      03  USU-RESTO-WS                PIC X(06).
000000* CODIGO USUARIO
000000   02  WS-DATA-INT-EXC.
000000      03 WS-SEC-EXC               PIC 9(02) COMP.
000000      03 WS-ANO-EXC               PIC 9(02) COMP.
000000      03 WS-MES-EXC               PIC 9(02) COMP.
000000      03 WS-DIA-EXC               PIC 9(02) COMP.
000000*  DATA SISTEMA
000000   02 DATA-ACCEPT            PIC 9(06)     COMP.
000000   02 DATA-ACCEPTR REDEFINES DATA-ACCEPT   COMP.
000000      03 ANO-ACCEPT          PIC 9(02)     COMP.
000000      03 MES-ACCEPT          PIC 9(02)     COMP.
000000      03 DIA-ACCEPT          PIC 9(02)     COMP.
000000
000000   02 DATA-SISTEMA           PIC 9(08)      COMP.
000000   02 DATA-SISTEMAR  REDEFINES DATA-SISTEMA COMP.
000000      03 SEC-SISTEMA         PIC 9(02)      COMP.
000000      03 ANO-SISTEMA         PIC 9(02)      COMP.
000000      03 MES-SISTEMA         PIC 9(02)      COMP.
000000      03 DIA-SISTEMA         PIC 9(02)      COMP.
000000
000000*----------------------------------------------------------------*
000000*   AREA COM OPCOES E INFORMACOES DE ENTRADA E SAIDA             *
000000*   PARA A LIBRARY DE TRATAMENTO DE EXCECAO DMS                  *
000000*----------------------------------------------------------------*
000000
000000 01  DMS-PARAMETROS-WS                                   COMMON.
000000
000000*    -----  PARAMETROS DE ENTRADA  -----
000000
000000     03  DMS-INTERFACE-COMS-WS       PIC X(01).
000000     03  DMS-TITLEBD-WS              PIC X(60).
000000     03  DMS-RESULT-WS               PIC X(06).
000000     03  DMS-PROG-ONLINE-WS          PIC X(01).
000000
000000*    -----  PARAMETROS DE SAIDA  -----
000000
000000     03  DMS-ACAO-TOMAR-WS           PIC 9(01).
000000     88  DMS-CONTINUAR               VALUE   01.
000000     88  DMS-ENCERRAR                VALUE   02.
000000     88  DMS-ATIVAR-DMTERMINATE      VALUE   03.
000000
000000*----------------------------------------------------------------*
000000* VALORES ASSUMIDOS PELOS CAMPOS DO HEADER E USADOS NO PROGRAMA  *
000000*----------------------------------------------------------------*
000000
000000 01 VALORES-STATUSVALUE-IN USAGE BINARY.
000000    03 FINALIZAR                    PIC S9(11) BINARY VALUE 99.
000000
000000 01  VALORES-STATUSVALUE-OUT       USAGE BINARY.
000000     03  SEND-SEM-ERRO             PIC S9(11) BINARY VALUE 00.
000000
000000*---------------------------------------------------------------*
000000*       CAMPOS DO HEADER PARA PASSAGEM DE PARAMETROS            *
000000*---------------------------------------------------------------*
000000
000000 01  ARRAY-IN-WS                     USAGE REAL COMMON    .
000000     03 PROGRAMDESG-WS               REAL                 .
000000     03 FUNCTIONINDEX-WS             REAL                 .
000000     03 USERCODE-WS                  REAL                 .
000000     03 SECURITYDESG-WS              REAL                 .
000000     03 FIELDS-WS                    REAL                 .
000000     03 TIMESTAMP-WS                 REAL                 .
000000     03 STATION-WS                   REAL                 .
000000     03 TEXTLENGTH-WS                REAL                 .
000000     03 FILLER-WS                    REAL                 .
000000     03 STATUSVALUE-WS               REAL                 .
000000     03 RESTART-WS                   REAL                 .
000000     03 AGENDA-WS                    REAL                 .
000000     03 SDFINFO-WS                   REAL                 .
000000     03 FORM-KEY-WS                  REAL                 .
000000     03 SDFTRANSNUM-WS               REAL                 .
000000     03 SDFFORMRECNUM-WS             REAL                 .
000000
000000*----------------------------------------------------------------*
000000* DECLARACAO DE AREAS QUE IRAO RECEBER/FORNECER MSD DO/PARA COMS.*
000000* O PROGRAMA RECEBE A MENSAGEM NA AREA "AREA-IN-WS", DEPOIS MOVE *
000000* ESTA AREA PARA A AREA DE TELA GERADA PELO SDF.                 *
000000******************************************************************
000000
000000* 01  AREA-IN-WS                      PIC X(28224)         COMMON.
000000
000000*----------------------------------------------------------------*
000000*    AREA AUXILIAR.                                              *
000000*----------------------------------------------------------------*
000000
000000** 01  FML-SISEG       FROM    DICTIONARY.
000000
000000*- LIBRARY COM OS DADOS OBRIGATORIOS PARA EXECUCAO DO PRG. COMS --
000000
000000 COMMUNICATION   SECTION.
000000
000000*----------------------------------------------------------------*
000000*                                                                *
000000* DECLARACAO INPUT E OUTPUT HEADER USADOS PELO COMS.             *
000000*                                                                *
000000*----------------------------------------------------------------*
000000
000000 INPUT HEADER COMS-IN
000000     CONVERSATION AREA.
000000     02 CONVERSATION-AREA    REAL.
000000        03 FORM-KEY-IN       REAL.
000000* OUTPUT HEADER COMS-OUT
000000*     CONVERSATION AREA.
000000*     02 CONVERSATION-AREA.
000000*        03 FORM-KEY-OUT      REAL.
000000*        03 PAGINADOR-OUT     REAL.
000000  OUTPUT HEADER COMS-OUT
000000  CONVERSATION AREA.
000000  02 CONVERSATION-AREA.
000000     03 FORM-KEY-OUT      REAL.
000000     03 PAGINADOR-OUT     PIC S9(11) BINARY EXTENDED.
000000     03 AGENDA-OUT        REAL.
000000     03 FUNCTIONINDEX-OUT REAL.
000000
000000******************************************************************
000000*                                                                *
000000*              CORPO PRINCIPAL DO PROGRAMA                       *
000000*                                                                *
000000******************************************************************
000000
000000 PROCEDURE DIVISION.
000000
000000 0001-00-PROGRAMA    SECTION.
000000
000000 0002-00-INICIO-TRATA-RELATORIO.
000000
000000     CHANGE ATTRIBUTE LIBACCESS OF "DCILIBRARY" TO BYFUNCTION.
000000*
000000     CHANGE ATTRIBUTE FUNCTIONNAME OF "DCILIBRARY" TO
000000                                      "COMSSUPPORT".
000000*
000000     ENABLE INPUT COMS-IN KEY "ONLINE".
000000*
000000     MOVE    PROGRAMDESG OF COMS-IN  TO  PROGRAMA-DESIGNATOR-77.
000000     MOVE    "A_S094765601"          TO  AGENDA-NOME-WS.
000000*
000000     CALL   "INICIALIZA_HOST_COMS OF S0997/OBJ/LIBRARY/INSTALACAO"
000000             USING   AGENDA-NOME-WS
000000                     PROGRAMA-DESIGNATOR-77
000000                     AGENDA-DESTINO-77
000000                     AGENDA-INPUT-77
000000                     WINDOW-DESIGNATOR-77
000000                     MENSAGEM-WS
000000             GIVING  RESULTADO-77.
000000*
000000     IF  RESULTADO-77    =  00
000000         MOVE    "BANCO-BDDATAMEC-<HOST>-<USERCODE>" TO
000000                 IDENTIFICADOR-GETINFO
000000         CALL    "GET_INSTALLATION_INFO OF S0997/OBJ/SLIBRARIES"
000000                 USING IDENTIFICADOR-GETINFO
000000                 GIVING RESULTADO-77.
000000
000000
000000    IF  RESULTADO-77    NOT =   0
000000             MOVE    "*** ERRO NA OBTENCAO DO TITULO DO BANCO"
000000                                      TO MENSAGEM-WS
000000             MOVE 1                   TO DESTCOUNT   OF COMS-OUT
000000             MOVE 0                   TO STATUSVALUE OF COMS-OUT
000000             MOVE 178                 TO TEXTLENGTH  OF COMS-OUT
000000             MOVE STATION OF COMS-IN  TO
000000                                      DESTINATIONDESG OF COMS-OUT
000000             MOVE MENSAGEM-WS         TO      LINHA-MENSAGEM-WS
000000             SEND COMS-OUT            FROM    TELA-MENSAGEM-WS
000000             GO TO 99900-00-ULTIMO-PARAGRAFO.
000000
000000     CHANGE ATTRIBUTE TITLE OF BDSEGURANCA  TO CONTEUDO-GETINFO.
000000
000000     OPEN INQUIRY BDSEGURANCA.
000000
000000  003-INICIO-REL.
000000
000000     INITIALIZE VARIAVEIS.
000000
000000     ACCEPT DATA-ACCEPT FROM DATE.
000000     MOVE DIA-ACCEPT TO DIA-SISTEMA.
000000     MOVE MES-ACCEPT TO MES-SISTEMA.
000000     MOVE ANO-ACCEPT TO ANO-SISTEMA.
000000     MOVE 20         TO SEC-SISTEMA.
000000
000000     INITIALIZE SV-AREA-REL.
000000
000000     RECEIVE COMS-IN MESSAGE INTO SV-AREA-REL.
000000     IF STATUSVALUE OF COMS-IN = FINALIZAR
000000        STOP RUN.
000000
000000     MOVE STATION OF COMS-IN TO ESTACAO-REAL-77.
000000     MOVE 12 TO CODIGO-INFORMACAO-77.
000000     CALL
000000       "SEG_OBTEM_INFORMACOES OF S0997/OBJ/LIBRARY/SEGURANCA/NOVA"
000000           USING ESTACAO-REAL-77
000000           CODIGO-INFORMACAO-77
000000           CLI-JANELA-SEG-WS
000000           GIVING RESULTADO-77.
000000
000000     MOVE SPACES TO MENSAGEM OF SV-AREA-REL
000000           MENSAGEM-RESULTADO OF SV-AREA-REL.
000000
000000     PERFORM 0100-IDENTIFICA-USU
000000        THRU 0100-IDENTIFICA-USU-FIM.
000000
000000     PERFORM 0110-MAC-ORIGINAL
000000        THRU 0110-MAC-ORIGINAL-FIM.
000000
000000     PERFORM 0120-MAC-SECUNDARIA
000000        THRU 0120-MAC-SECUNDARIA-FIM.
000000
000000     PERFORM 0130-MEX
000000        THRU 0130-MEX-FIM.
000000
000000*     IF MENSAGEM OF SV-AREA-REL NOT EQUAL SPACES
000000*      IF CONT-GRUPOS OF SV-AREA-REL = 0
000000*       AND CONT-PERFIL OF SV-AREA-REL = 0
000000*         MOVE 1 TO CODIGO-RESULTADO OF SV-AREA-REL.
000000
000000*      DISPLAY "CONT-GRUPOS:" CONT-GRUPOS OF SV-AREA-REL.
000000*      DISPLAY "CON-PERFUL: " CONT-PERFIL OF SV-AREA-REL.
000000*      DISPLAY "COD-RESULT: " CODIGO-RESULTADO OF SV-AREA-REL.
000000
000000     GO TO 003-INICIO-REL.
000000*   STOP RUN.
000000  003-FIM-REL.
000000     EXIT.
000000
000000******************************************************************
000000*    PROCESSAMENTO IDENTIFICA PARAMETROS DO USUARIO              *
000000******************************************************************
000000
000000  0100-IDENTIFICA-USU .
000000/* MOVIMENTACAO DO CODIGO USUARIO PARA VAR-AUX
000000        MOVE USU-CODIGO OF SV-AREA-REL TO USU-CODIGO-WS.
000000/* VALIDACAO DO USUARIO NA USUARIO-CLI
000000        PERFORM 0200-ACESSO-USUARIOS-CLI
000000           THRU 0210-ACESSO-USUARIOS-CLI-FIM.
000000
000000        PERFORM 0250-VERIFICA-FIS-SUB
000000           THRU 0250-VERIFICA-FIS-SUB-FIM.
000000
000000        PERFORM 0240-VERIFICA-MEX
000000           THRU 0240-VERIFICA-MEX-FIM.
000000
000000  0100-IDENTIFICA-USU-FIM.
000000     EXIT.
000000
000000******************************************************************
000000*    PROCESSAMENTO DA MAC ORIGINAL                               *
000000******************************************************************
000000
000000  0110-MAC-ORIGINAL.
000000/*BUSCA LOTACAO
000000
000000        MOVE "ORI" TO TIPO-AREA OF SV-AREA-REL.
000000        MOVE "S" TO FLAG-FIM-AREA OF SV-AREA-REL.
000000
000000        IF FLAG-MAC-SEC = "S" OR FLAG-MEX = "S"
000000           MOVE "N" TO FLAG-FIM-AREA OF SV-AREA-REL
000000        END-IF.
000000**********************************************************
000000*        MOVE "S" TO FLAG-FIM-AREA OF SV-AREA-REL.
000000**********************************************************
000000        MOVE UNO-CODIGO-ORI OF USUARIO-CLI TO UNO-CODIGO-WS.
000000        MOVE FCO-CODIGO-ORI OF USUARIO-CLI TO FCO-CODIGO-WS.
000000
000000        PERFORM 0300-BUSCA-TIPO-LOTACAO
000000           THRU 0310-BUSCA-TIPO-LOTACAO-FIM.
000000
000000*BG240414 - D550134I
000000       IF IND-FUNC-WS EQUAL "S"
000000         PERFORM 0400-BUSCA-TIPO-FUNCAO
000000            THRU 0410-BUSCA-TIPO-FUNCAO-FIM
000000       ELSE
000000         MOVE TIF-CODIGO OF USUARIO-CLI TO FCO-CODIGO-WS
000000         PERFORM 0400-BUSCA-TIP-FUN-USU-N-CEF
000000            THRU 0410-FIM-TIP-FUN-USU-N-CEF-FIM.
000000*BG240414 - D550134F
000000
000000        PERFORM 0500-ACESSO-MATRIZ-GRUPO
000000           THRU 0500-ACESSO-MATRIZ-GRUPO-FIM.
000000
000000        PERFORM 0600-MONTA-PERFIL
000000           THRU 0600-MONTA-PERFIL-FIM.
000000
000000        COMPUTE TAMANHO-AREA-WS =
000000             FUNCTION LENGTH(AREA-HEADER) +
000000             FUNCTION LENGTH(AREA-PARAMETROS) +
000000             FUNCTION LENGTH(AREA-APLICATIVO) +
000000             FUNCTION LENGTH(TAB-GRUPOS(1)) * CONT-GRUPOS.
000000
000000        IF CONT-GRUPOS OF SV-AREA-REL = 0
000000            AND CONT-PERFIL OF SV-AREA-REL = 0
000000               MOVE 1 TO CODIGO-RESULTADO OF SV-AREA-REL.
000000
000000        PERFORM 0100-00-ENVIA-TELA
000000           THRU 0100-00-ENVIA-TELA-FIM.
000000
000000  0110-MAC-ORIGINAL-FIM.
000000       EXIT.
000000
000000******************************************************************
000000*    PROCESSAMENTO DA MAC TEMPORARIA/SUBSTITUTA                  *
000000******************************************************************
000000
000000  0120-MAC-SECUNDARIA.
000000
000000        IF FLAG-MAC-SEC = "N"
000000           GO TO 0120-MAC-SECUNDARIA-FIM.
000000
000000        IF FLAG-MEX = "S"
000000           MOVE "N" TO FLAG-FIM-AREA OF SV-AREA-REL
000000        ELSE
000000           MOVE "S" TO FLAG-FIM-AREA OF SV-AREA-REL
000000        END-IF.
000000
000000*D550134 - BG240415 - INICIO VALIDADE SUBSTITUICAO
000000     MOVE USC-SEC-INI-SUBST OF USUARIO-CLI
000000                          TO SEC-VALI-INI OF SV-AREA-REL.
000000     MOVE USC-ANO-INI-SUBST OF USUARIO-CLI
000000                          TO ANO-VALI-INI OF SV-AREA-REL.
000000     MOVE USC-MES-INI-SUBST OF USUARIO-CLI
000000                          TO MES-VALI-INI OF SV-AREA-REL.
000000     MOVE USC-DIA-INI-SUBST OF USUARIO-CLI
000000                          TO DIA-VALI-INI OF SV-AREA-REL.
000000*D550134 - BG240415
000000
000000        MOVE USC-SEC-FIM-SUBST OF USUARIO-CLI
000000                               TO SEC-VALIDADE OF SV-AREA-REL.
000000        MOVE USC-ANO-FIM-SUBST OF USUARIO-CLI
000000                               TO ANO-VALIDADE OF SV-AREA-REL.
000000        MOVE USC-MES-FIM-SUBST OF USUARIO-CLI
000000                               TO MES-VALIDADE OF SV-AREA-REL.
000000        MOVE USC-DIA-FIM-SUBST OF USUARIO-CLI
000000                               TO DIA-VALIDADE OF SV-AREA-REL.
000000
000000        MOVE TIPO-CODIGO-SEC-WS TO TIPO-AREA OF SV-AREA-REL.
000000
000000        MOVE UNO-CODIGO-SEC-WS TO UNO-CODIGO-WS.
000000        MOVE FCO-CODIGO-SEC-WS TO FCO-CODIGO-WS.
000000
000000        PERFORM 0300-BUSCA-TIPO-LOTACAO
000000               THRU 0310-BUSCA-TIPO-LOTACAO-FIM.
000000
000000        PERFORM 0400-BUSCA-TIPO-FUNCAO
000000               THRU 0410-BUSCA-TIPO-FUNCAO-FIM.
000000
000000        PERFORM 0500-ACESSO-MATRIZ-GRUPO
000000              THRU 0500-ACESSO-MATRIZ-GRUPO-FIM.
000000
000000        PERFORM 0600-MONTA-PERFIL
000000           THRU 0600-MONTA-PERFIL-FIM.
000000
000000        COMPUTE TAMANHO-AREA-WS =
000000             FUNCTION LENGTH(AREA-HEADER) +
000000             FUNCTION LENGTH(AREA-PARAMETROS) +
000000             FUNCTION LENGTH(AREA-APLICATIVO) +
000000             FUNCTION LENGTH(TAB-GRUPOS(1)) * CONT-GRUPOS.
000000
000000        IF CONT-GRUPOS OF SV-AREA-REL = 0
000000            AND CONT-PERFIL OF SV-AREA-REL = 0
000000               MOVE 1 TO CODIGO-RESULTADO OF SV-AREA-REL.
000000
000000        PERFORM 0100-00-ENVIA-TELA
000000           THRU 0100-00-ENVIA-TELA-FIM.
000000
000000  0120-MAC-SECUNDARIA-FIM.
000000       EXIT.
000000
000000******************************************************************
000000*    PROCESSAMENTO DA MAC DE EXCECAO                             *
000000******************************************************************
000000
000000  0130-MEX.
000000
000000        IF FLAG-MEX = "N"
000000           GO TO 0130-MEX-FIM.
000000
000000        MOVE "S" TO FLAG-FIM-AREA OF SV-AREA-REL.
000000        MOVE "MEX" TO TIPO-AREA OF SV-AREA-REL.
000000
000000        PERFORM 0700-MONTA-MEX
000000           THRU 0700-MONTA-MEX-FIM
000000
000000        PERFORM 0730-MONTA-PERFIL
000000           THRU 0730-MONTA-PERFIL-FIM.
000000
000000        COMPUTE TAMANHO-AREA-WS =
000000             FUNCTION LENGTH(AREA-HEADER) +
000000             FUNCTION LENGTH(AREA-PARAMETROS) +
000000             FUNCTION LENGTH(AREA-APLICATIVO) +
000000             FUNCTION LENGTH(TAB-GRUPOS(1)) * CONT-GRUPOS.
000000
000000        IF CONT-GRUPOS OF SV-AREA-REL = 0
000000            AND CONT-PERFIL OF SV-AREA-REL = 0
000000               MOVE 1 TO CODIGO-RESULTADO OF SV-AREA-REL.
000000
000000        PERFORM 0100-00-ENVIA-TELA
000000           THRU 0100-00-ENVIA-TELA-FIM.
000000
000000  0130-MEX-FIM.
000000       EXIT.
000000
000000******************************************************************
000000*    ENVIO DAS INFORMACOES DE RESPOSTA                           *
000000******************************************************************
000000
000000   0100-00-ENVIA-TELA.
000000
000000     MOVE 1                    TO DESTCOUNT OF COMS-OUT.
000000     MOVE AGENDA OF COMS-IN    TO AGENDA-OUT OF COMS-OUT.
000000     MOVE STATION OF COMS-IN   TO DESTINATIONDESG OF COMS-OUT.
000000     MOVE FUNCTIONINDEX        OF COMS-IN
000000     TO   FUNCTIONINDEX-OUT    OF COMS-OUT.
000000     MOVE TAMANHO-AREA-WS      TO TEXTLENGTH OF COMS-OUT.
000000
000000     MOVE ZEROS TO FORM-KEY-OUT.
000000
000000     SEND COMS-OUT FROM SV-AREA-REL.
000000
000000     IF  STATUSVALUE OF COMS-OUT NOT = ZEROS
000000         MOVE STATUSVALUE      OF COMS-OUT TO RESULTADO-77
000000         MOVE FORM-KEY-IN      OF COMS-IN TO FORM-KEY-WS
000000         MOVE SDFINFO          OF COMS-IN TO SDFINFO-WS
000000         MOVE PROGRAMDESG      OF COMS-IN TO PROGRAMDESG-WS
000000         MOVE FUNCTIONINDEX    OF COMS-IN TO FUNCTIONINDEX-WS
000000         MOVE USERCODE         OF COMS-IN TO USERCODE-WS
000000         MOVE SECURITYDESG     OF COMS-IN TO SECURITYDESG-WS
000000         MOVE TIMESTAMP        OF COMS-IN TO TIMESTAMP-WS
000000         MOVE STATION          OF COMS-IN TO STATION-WS
000000         MOVE TEXTLENGTH       OF COMS-IN TO TEXTLENGTH-WS
000000         MOVE STATUSVALUE      OF COMS-IN TO STATUSVALUE-WS
000000         MOVE RESTART          OF COMS-IN TO RESTART-WS
000000         MOVE AGENDA           OF COMS-IN TO AGENDA-WS
000000         CALL   "TRATA_ERRO_SAIDA OF S0997/OBJ/LIBRARY/INSTALACAO"
000000                 USING   ARRAY-IN-WS
000000                         PROGRAMA-DESIGNATOR-77
000000                         RESULTADO-77.
000000
000000   0100-00-ENVIA-TELA-FIM.
000000      EXIT.
000000
000000*****************************************************************
000000*                                                               *
000000*           INICIO PROCESSAMENTO - RELATORIO MAC/MEX            *
000000*                                                               *
000000*****************************************************************
000000
000000 0200-ACESSO-USUARIOS-CLI.
000000*  FIND PARA COMPARAR DATA DO SISTEMA COM USC-DATA-INI-SUBST
000000
000000       FIND FIRST USC-POR-USUARIO AT
000000          CLI-CODIGO = CLI-JANELA-SEG-WS AND
000000          USU-CODIGO = USU-CODIGO OF SV-AREA-REL
000000          ON EXCEPTION
000000              IF DMSTATUS(NOTFOUND)
000000                 MOVE "Usuario nao possui MAC." TO
000000                 MENSAGEM OF SV-AREA-REL
000000              ELSE
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                     THRU 10000-99-TRATA-ERRO-DMS
000000              GO TO 0210-ACESSO-USUARIOS-CLI-FIM
000000              END-IF
000000       END-FIND.
000000
000000*BG240414 - D550134I
000000
000000       MOVE "S" TO IND-FUNC-WS.
000000** se janela 103 mover pra 102
000000       IF CLI-JANELA-SEG-WS EQUAL 103
000000          MOVE 102 TO CLI-JANELA-FUN
000000       ELSE
000000          MOVE CLI-JANELA-SEG-WS TO CLI-JANELA-FUN.
000000
000000       FIND FIRST FUN-POR-IDENT AT
000000          FUN-CLIENTE = CLI-JANELA-FUN  AND
000000          FUN-CODIGO = USU-CODIGO OF SV-AREA-REL
000000          ON EXCEPTION
000000            IF DMSTATUS(NOTFOUND)
000000               MOVE "N" TO IND-FUNC-WS
000000               MOVE "Usuario nao e funcionario." TO
000000                 MENSAGEM OF SV-AREA-REL
000000            ELSE
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                     THRU 10000-99-TRATA-ERRO-DMS
000000            END-IF
000000            GO TO 0210-ACESSO-USUARIOS-CLI-FIM
000000       END-FIND.
000000
000000*BG240414 - D550134F
000000
000000
000000  0210-ACESSO-USUARIOS-CLI-FIM.
000000      EXIT.
000000
000000
000000  0240-VERIFICA-MEX.
000000
000000     MOVE "N" TO FLAG-MEX.
000000
000000     IF USC-SEC-FIM-EXC EQUAL ZEROS
000000        GO TO 0240-VERIFICA-MEX-FIM.
000000
000000     IF DATA-SISTEMAR < USC-DATA-INI-EXC OF USUARIO-CLI
000000        GO TO 0240-VERIFICA-MEX-FIM.
000000
000000     IF DATA-SISTEMAR > USC-DATA-FIM-EXC OF USUARIO-CLI
000000        GO TO 0240-VERIFICA-MEX-FIM.
000000
000000     MOVE "S" TO FLAG-MEX.
000000
000000  0240-VERIFICA-MEX-FIM.
000000     EXIT.
000000
000000  0250-VERIFICA-FIS-SUB.
000000**VERIFICA TIPO USUARIO E UNO-CODIGO
000000
000000   MOVE "N" TO FLAG-MAC-SEC.
000000
000000   IF USU-LETRA-WS NOT EQUAL "C"
000000      GO TO 0250-VERIFICA-FIS-SUB-FIM.
000000
000000   IF UNO-CODIGO-SUB OF USUARIO-CLI NOT EQUAL ZEROS AND
000000      DATA-SISTEMAR >= USC-DATA-INI-SUBST OF USUARIO-CLI AND
000000      DATA-SISTEMAR <= USC-DATA-FIM-SUBST OF USUARIO-CLI THEN
000000
000000      MOVE UNO-CODIGO-SUB OF USUARIO-CLI TO UNO-CODIGO-SEC-WS
000000      MOVE FCO-CODIGO-SUB OF USUARIO-CLI TO FCO-CODIGO-SEC-WS
000000      MOVE "S" TO FLAG-MAC-SEC
000000      MOVE "SUB" TO TIPO-CODIGO-SEC-WS
000000      GO TO 0250-VERIFICA-FIS-SUB-FIM
000000    END-IF.
000000
000000   IF UNO-CODIGO-FIS OF USUARIO-CLI NOT EQUAL ZEROS THEN
000000     MOVE UNO-CODIGO-FIS OF USUARIO-CLI TO UNO-CODIGO-SEC-WS
000000     MOVE FCO-CODIGO-FIS OF USUARIO-CLI TO FCO-CODIGO-SEC-WS
000000     MOVE "S" TO FLAG-MAC-SEC
000000     MOVE "FIS" TO TIPO-CODIGO-SEC-WS
000000   END-IF.
000000
000000 0250-VERIFICA-FIS-SUB-FIM.
000000   EXIT.
000000
000000**VERIFICA TIPO USUARIO E UNO-CODIGO
000000
000000 0300-BUSCA-TIPO-LOTACAO.
000000
000000**PROCURA LOT-CODIGO
000000   FIND UNL-POR-UNO AT
000000         CLI-CODIGO = CLI-JANELA-SEG-WS AND
000000         UNO-CODIGO = UNO-CODIGO-WS
000000         ON EXCEPTION
000000            IF NOT DMSTATUS (NOTFOUND) THEN
000000               PERFORM 10000-00-TRATA-ERRO-DMS
000000                  THRU 10000-99-TRATA-ERRO-DMS
000000            ELSE
000000               MOVE "Tipo de Lotacao nao cadastrada." TO
000000                 MENSAGEM OF SV-AREA-REL
000000            END-IF
000000            GO TO 0310-BUSCA-TIPO-LOTACAO-FIM
000000    END-FIND.
000000**PROCURA LOT-CODIGO
000000
000000**MOVE LOT-CODIGO
000000    MOVE LOT-CODIGO OF UNO-LOTACAO
000000      TO LOT-CODIGO OF SV-AREA-REL.
000000
000000  0310-BUSCA-TIPO-LOTACAO-FIM.
000000  EXIT.
000000
000000  0400-BUSCA-TIPO-FUNCAO.
000000
000000**PROCURA TIF-CODIGO
000000
000000    FIND FIRST FCO-POR-CODIGO AT
000000           CLI-CODIGO   =  CLI-JANELA-SEG-WS AND
000000           FCO-CODIGO   =  FCO-CODIGO-WS
000000           ON EXCEPTION
000000              IF DMSTATUS (NOTFOUND)
000000                 MOVE "Tipo de Funcao nao cadastrada."
000000                   TO MENSAGEM OF SV-AREA-REL
000000                 MOVE 1 TO RESULTADO-77
000000              ELSE
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                    THRU 10000-99-TRATA-ERRO-DMS
000000              END-IF
000000              GO TO 0410-BUSCA-TIPO-FUNCAO-FIM
000000      END-FIND.
000000**PROCURA TIF-CODIGO
000000**MOVE TIF-CODIGO
000000      MOVE TIF-CODIGO OF FUNCAO
000000         TO TIF-CODIGO OF SV-AREA-REL.
000000
000000****** MOVE FCO-CODIGO OF FUNCAO
000000*******  TO FCO-CODIGO OF SV-AREA-REL.
000000
000000**MOVE TIF-CODIGO
000000  0410-BUSCA-TIPO-FUNCAO-FIM.
000000  EXIT.
000000
000000  0400-BUSCA-TIP-FUN-USU-N-CEF.
000000
000000**PROCURA TIF-CODIGO
000000*TIF-CODIGO SUBSET KEYS CLI-CODIGO, TIF-CODIGO
000000    FIND FIRST TIF-POR-CODIGO AT
000000           CLI-CODIGO   =  CLI-JANELA-SEG-WS AND
000000           TIF-CODIGO   =  FCO-CODIGO-WS
000000           ON EXCEPTION
000000              IF DMSTATUS (NOTFOUND)
000000                 MOVE "Tipo de Funcao nao cadastrada."
000000                   TO MENSAGEM OF SV-AREA-REL
000000                 MOVE 1 TO RESULTADO-77
000000              ELSE
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                    THRU 10000-99-TRATA-ERRO-DMS
000000              END-IF
000000              GO TO 0410-FIM-TIP-FUN-USU-N-CEF-FIM
000000      END-FIND.
000000**PROCURA TIF-CODIGO
000000**MOVE TIF-CODIGO
000000      MOVE TIF-CODIGO OF TIPO-FUNCAO
000000         TO TIF-CODIGO OF SV-AREA-REL.
000000
000000**MOVE TIF-CODIGO
000000  0410-FIM-TIP-FUN-USU-N-CEF-FIM.
000000  EXIT.
000000
000000****************************************************************
000000*                                                              *
000000*                        MONTAGEM MAC                          *
000000*                                                              *
000000****************************************************************
000000****************************************************************
000000***                      GRUPO MAC                           ***
000000****************************************************************
000000  0500-ACESSO-MATRIZ-GRUPO.
000000* PROCURAR MATRIZ PELA LOTACAO
000000    SET MGR-POR-LOTAC TO BEGINNING.
000000    SET SIS-POR-CODIGO TO BEGINNING.
000000
000000    MOVE ZEROS TO CONT-GRUPOS OF SV-AREA-REL.
000000  0501-MONTA-MATRIZ-GRUPO.
000000    FIND KEY OF NEXT MGR-POR-LOTAC AT
000000         CLI-CODIGO = CLI-JANELA-SEG-WS AND
000000         LOT-CODIGO = LOT-CODIGO  OF SV-AREA-REL    AND
000000         TIF-CODIGO = TIF-CODIGO  OF SV-AREA-REL
000000         ON EXCEPTION
000000            IF NOT DMSTATUS(NOTFOUND)
000000               PERFORM 10000-00-TRATA-ERRO-DMS
000000                  THRU 10000-99-TRATA-ERRO-DMS
000000            ELSE
000000               MOVE "Usuario nao possui matriz de acesso." TO
000000                 MENSAGEM OF SV-AREA-REL
000000            END-IF
000000            GO TO 0500-ACESSO-MATRIZ-GRUPO-FIM
000000    END-FIND.
000000
000000    MOVE GOP-CODIGO OF MATRIZ-GRUPO TO GOP-CODIGO-WS.
000000    MOVE SIS-CODIGO OF MATRIZ-GRUPO TO SIS-CODIGO-WS.
000000
000000    IF CONT-SISTEMA = 0
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000    IF SIS-CODIGO OF TAB-SISTEMA(CONT-SISTEMA)
000000                        NOT EQUAL SIS-CODIGO OF MATRIZ-GRUPO
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000    PERFORM 0550-MONTA-GRUPO THRU 0550-MONTA-GRUPO-FIM.
000000
000000    GO TO 0501-MONTA-MATRIZ-GRUPO.
000000
000000 0500-ACESSO-MATRIZ-GRUPO-FIM.
000000   EXIT.
000000
000000 0520-MONTA-SISTEMA.
000000* PROCURA SISTEMA
000000    IF TAB-SISTEMA-OK(SIS-CODIGO-WS) NOT EQUAL ZEROS
000000        GO TO 0545-MONTA-SISTEMA-FIM.
000000
000000    IF CONT-SISTEMA >= 40
000000       MOVE "Limite da tabela de SISTEMAS excedido"
000000         TO MENSAGEM OF SV-AREA-REL
000000       GO TO 0545-MONTA-SISTEMA-FIM
000000    END-IF
000000
000000    ADD 1 TO CONT-SISTEMA.
000000
000000    MOVE SPACES TO SIS-NOME OF SV-AREA-REL(CONT-SISTEMA).
000000
000000    FIND NEXT SIS-POR-CODIGO AT
000000          SIS-CODIGO = SIS-CODIGO-WS
000000           ON EXCEPTION
000000              IF DMSTATUS(NOTFOUND)
000000                 MOVE "Sistema nao cadastrado."
000000                   TO MENSAGEM OF SV-AREA-REL
000000              ELSE
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                    THRU 10000-99-TRATA-ERRO-DMS
000000              END-IF
000000     END-FIND.
000000
000000     MOVE SIS-CODIGO OF SISTEMAS
000000        TO SIS-CODIGO OF SV-AREA-REL(CONT-SISTEMA).
000000
000000     MOVE 1 TO TAB-SISTEMA-OK(SIS-CODIGO-WS).
000000
000000     MOVE SIS-NOME OF SISTEMAS
000000          TO SIS-NOME OF SV-AREA-REL(CONT-SISTEMA).
000000
000000  0530-MONTA-COD-ABRANG.
000000/*MONTAGEM DA ABRANGENCIA
000000
000000     FIND MAB-POR-LOT-TIF AT
000000           CLI-CODIGO   = CLI-JANELA-SEG-WS AND
000000           SIS-CODIGO   = SIS-CODIGO OF SISTEMAS        AND
000000           LOT-CODIGO   = LOT-CODIGO OF SV-AREA-REL     AND
000000           TIF-CODIGO   = TIF-CODIGO OF SV-AREA-REL
000000           ON EXCEPTION
000000              IF NOT DMSTATUS (NOTFOUND)
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                    THRU 10000-99-TRATA-ERRO-DMS
000000              END-IF
000000            GO TO 0545-MONTA-SISTEMA-FIM
000000     END-FIND.
000000
000000     IF UCS-ABRANGENCIA OF MATRIZ-ABRANG EQUAL ZEROS
000000         GO TO 0545-MONTA-SISTEMA-FIM.
000000
000000     MOVE UCS-ABRANGENCIA OF MATRIZ-ABRANG
000000       TO UCS-ABRANGENCIA OF SV-AREA-REL(CONT-SISTEMA).
000000
000000  0540-MONTA-NOME-ABRANG.
000000
000000    FIND FIRST TAG-POR-IDENT AT
000000          TAG-CLIENTE  =  CLI-CODIGO  OF USUARIO-CLI      AND
000000          TAG-SISTEMA  =  SIS-CODIGO  OF MATRIZ-GRUPO      AND
000000          TAG-CODIGO   =  UCS-ABRANGENCIA OF MATRIZ-ABRANG
000000           ON EXCEPTION
000000              IF DMSTATUS (NOTFOUND)
000000                 MOVE "Codigo abrangencia nao cadastrado"
000000                   TO ABR-DESCRICAO OF SV-AREA-REL(CONT-SISTEMA)
000000              ELSE
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                    THRU 10000-99-TRATA-ERRO-DMS
000000              END-IF
000000            GO TO 0545-MONTA-SISTEMA-FIM
000000    END-FIND.
000000
000000    MOVE TAG-DESCRICAO   OF TAB-ABRANGENCIA
000000      TO ABR-DESCRICAO   OF SV-AREA-REL(CONT-SISTEMA).
000000
000000  0545-MONTA-SISTEMA-FIM.
000000  EXIT.
000000
000000  0550-MONTA-GRUPO.
000000* MONTAGEM GRUPOS
000000
000000   FIND GOP-POR-IDENT AT
000000          CFS-VER-SISEG = VERSAO-SEG-WS     AND
000000          GOP-CLIENTE   = CLI-JANELA-SEG-WS AND
000000          GOP-SISTEMA   = SIS-CODIGO-WS    AND
000000          GOP-CODIGO    = GOP-CODIGO-WS
000000          ON EXCEPTION
000000             IF DMSTATUS (NOTFOUND)
000000                  MOVE "Grupo nao identificado"
000000                         TO MENSAGEM OF SV-AREA-REL
000000             ELSE
000000                PERFORM 10000-00-TRATA-ERRO-DMS
000000                   THRU 10000-99-TRATA-ERRO-DMS
000000             END-IF
000000          GO TO 0550-MONTA-GRUPO-FIM
000000     END-FIND.
000000
000000  0570-MOVE-DADOS.
000000
000000     IF CONT-GRUPOS >= 300
000000        MOVE "Limite da tabela de GRUPOS excedido"
000000          TO MENSAGEM OF SV-AREA-REL
000000        GO TO 0550-MONTA-GRUPO-FIM
000000     END-IF
000000
000000     ADD 1 TO CONT-GRUPOS.
000000     MOVE CONT-GRUPOS TO CONT-GRUPOS OF SV-AREA-REL.
000000
000000     MOVE SIS-CODIGO-WS
000000       TO SIS-CODIGO-GRP OF SV-AREA-REL(CONT-GRUPOS).
000000     MOVE GOP-DESCRICAO OF GRUPOS-OPERACOES
000000       TO GOP-DESCRICAO OF SV-AREA-REL(CONT-GRUPOS).
000000     MOVE GOP-MNEMONICO OF GRUPOS-OPERACOES
000000       TO GOP-MNEMONICO OF SV-AREA-REL(CONT-GRUPOS).
000000
000000  0550-MONTA-GRUPO-FIM.
000000  EXIT.
000000
000000******************************************************
000000***                 PERFIL MAC                     ***
000000******************************************************
000000
000000  0600-MONTA-PERFIL.
000000* MONTAGEM PERFIL
000000   MOVE ZEROS TO CONT-PERFIL OF SV-AREA-REL.
000000
000000   SET SIS-POR-CODIGO TO BEGINNING.
000000   SET MPE-POR-LOT-TIF TO BEGINNING.
000000
000000 0601-MONTA-MATRIZ-PERFIL.
000000    FIND KEY OF NEXT MPE-POR-LOT-TIF AT
000000         CLI-CODIGO = CLI-JANELA-SEG-WS AND
000000         LOT-CODIGO = LOT-CODIGO  OF SV-AREA-REL    AND
000000         TIF-CODIGO = TIF-CODIGO  OF SV-AREA-REL
000000         ON EXCEPTION
000000            IF NOT DMSTATUS(NOTFOUND)
000000               PERFORM 10000-00-TRATA-ERRO-DMS
000000                  THRU 10000-99-TRATA-ERRO-DMS
000000            END-IF
000000         GO TO 0600-MONTA-PERFIL-FIM
000000    END-FIND.
000000
000000    MOVE PER-CODIGO OF MATRIZ-PERFIL TO PER-CODIGO-WS.
000000    MOVE SIS-CODIGO OF MATRIZ-PERFIL TO SIS-CODIGO-WS.
000000
000000
000000    IF CONT-SISTEMA = 0
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000    IF SIS-CODIGO OF TAB-SISTEMA(CONT-SISTEMA)
000000                        NOT EQUAL SIS-CODIGO OF MATRIZ-PERFIL
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000    PERFORM 0610-COMPOR-PERFIL THRU 0625-FIM-PERFIL-MAC.
000000
000000    GO TO 0601-MONTA-MATRIZ-PERFIL.
000000
000000 0600-MONTA-PERFIL-FIM.
000000   EXIT.
000000
000000 0610-COMPOR-PERFIL.
000000
000000     FIND PER-POR-IDENT AT
000000          SIS-CODIGO = SIS-CODIGO-WS AND
000000          PER-CODIGO = PER-CODIGO-WS
000000          ON EXCEPTION
000000             IF NOT DMSTATUS (NOTFOUND)
000000                 PERFORM 10000-00-TRATA-ERRO-DMS
000000                   THRU 10000-99-TRATA-ERRO-DMS
000000             END-IF
000000             GO TO 0625-FIM-PERFIL-MAC
000000     END-FIND.
000000
000000 0620-MOVE-DADOS.
000000
000000     IF CONT-PERFIL >= 100
000000        MOVE "Limite da tabela de PERFIL excedido"
000000          TO MENSAGEM OF SV-AREA-REL
000000          GO TO 0625-FIM-PERFIL-MAC
000000     END-IF.
000000
000000     ADD 1 TO CONT-PERFIL.
000000
000000     MOVE SIS-CODIGO-WS
000000       TO SIS-CODIGO-PER OF SV-AREA-REL(CONT-PERFIL).
000000     MOVE PER-DESCRICAO OF PERFIL
000000       TO PER-DESCRICAO OF SV-AREA-REL(CONT-PERFIL).
000000     MOVE PER-CODIGO    OF PERFIL
000000       TO PER-CODIGO    OF SV-AREA-REL(CONT-PERFIL).
000000
000000
000000  0625-FIM-PERFIL-MAC.
000000   EXIT.
000000
000000****************************************************************
000000*                                                              *
000000*                        MONTAGEM MEX                          *
000000*                                                              *
000000****************************************************************
000000  0700-MONTA-MEX.
000000
000000     MOVE ZEROS TO CONT-GRUPOS OF SV-AREA-REL.
000000
000000     SET GRX-POR-USU TO BEGINNING.
000000     SET SIS-POR-CODIGO TO BEGINNING.
000000
000000*D550134 - BG240415 - INICIO VALIDADE EXCECAO
000000     MOVE USC-SEC-INI-EXC OF USUARIO-CLI
000000                          TO SEC-VALI-INI OF SV-AREA-REL.
000000     MOVE USC-ANO-INI-EXC OF USUARIO-CLI
000000                          TO ANO-VALI-INI OF SV-AREA-REL.
000000     MOVE USC-MES-INI-EXC OF USUARIO-CLI
000000                          TO MES-VALI-INI OF SV-AREA-REL.
000000     MOVE USC-DIA-INI-EXC OF USUARIO-CLI
000000                          TO DIA-VALI-INI OF SV-AREA-REL.
000000*D550134 - BG240415
000000
000000     MOVE USC-SEC-FIM-EXC OF USUARIO-CLI
000000                          TO SEC-VALIDADE OF SV-AREA-REL.
000000     MOVE USC-ANO-FIM-EXC OF USUARIO-CLI
000000                          TO ANO-VALIDADE OF SV-AREA-REL.
000000     MOVE USC-MES-FIM-EXC OF USUARIO-CLI
000000                          TO MES-VALIDADE OF SV-AREA-REL.
000000     MOVE USC-DIA-FIM-EXC OF USUARIO-CLI
000000                          TO DIA-VALIDADE OF SV-AREA-REL.
000000
000000   0701-MONTA-MATRIZ.
000000
000000    FIND KEY OF NEXT GRX-POR-USU AT
000000         CLI-CODIGO = CLI-JANELA-SEG-WS AND
000000         USU-CODIGO = USU-CODIGO OF SV-AREA-REL
000000         ON EXCEPTION
000000            IF NOT DMSTATUS(NOTFOUND)
000000               PERFORM 10000-00-TRATA-ERRO-DMS
000000                  THRU 10000-99-TRATA-ERRO-DMS
000000            END-IF
000000          GO TO 0700-MONTA-MEX-FIM
000000    END-FIND.
000000
000000    MOVE GOP-CODIGO OF GRUPO-EXC-USU TO GOP-CODIGO-WS.
000000    MOVE SIS-CODIGO OF GRUPO-EXC-USU TO SIS-CODIGO-WS.
000000
000000    IF CONT-SISTEMA = 0
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000    IF SIS-CODIGO OF TAB-SISTEMA(CONT-SISTEMA)
000000                        NOT EQUAL SIS-CODIGO OF GRUPO-EXC-USU
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000    PERFORM 0550-MONTA-GRUPO THRU 0550-MONTA-GRUPO-FIM.
000000
000000    GO TO 0701-MONTA-MATRIZ.
000000
000000  0700-MONTA-MEX-FIM.
000000   EXIT.
000000
000000******************************************************
000000***                 PERFIL MEX                     ***
000000******************************************************
000000
000000  0730-MONTA-PERFIL.
000000* MONTAGEM PERFIL MEX
000000     MOVE ZEROS TO CONT-PERFIL OF SV-AREA-REL.
000000
000000     SET SIS-POR-CODIGO TO BEGINNING.
000000     SET PRX-POR-USU-PER TO BEGINNING.
000000
000000  0710-MONTA-MATRIZ-EXC-PERFIL.
000000
000000     FIND NEXT PRX-POR-USU-PER AT
000000        CLI-CODIGO = CLI-JANELA-SEG-WS      AND
000000        USU-CODIGO = USU-CODIGO OF SV-AREA-REL
000000        ON EXCEPTION
000000          IF NOT DMSTATUS(NOTFOUND)
000000             PERFORM 10000-00-TRATA-ERRO-DMS
000000                THRU 10000-99-TRATA-ERRO-DMS
000000          END-IF
000000       GO TO 0730-MONTA-PERFIL-FIM
000000     END-FIND.
000000
000000    MOVE PER-CODIGO OF PERFIL-EXC-USU TO PER-CODIGO-WS.
000000    MOVE SIS-CODIGO OF PERFIL-EXC-USU TO SIS-CODIGO-WS.
000000
000000    IF CONT-SISTEMA = 0
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000    IF SIS-CODIGO OF TAB-SISTEMA(CONT-SISTEMA)
000000                        NOT EQUAL SIS-CODIGO OF GRUPO-EXC-USU
000000      PERFORM 0520-MONTA-SISTEMA THRU 0545-MONTA-SISTEMA-FIM.
000000
000000*    PERFORM 0600-MONTA-PERFIL THRU 0600-MONTA-PERFIL-FIM.
000000    PERFORM 0610-COMPOR-PERFIL THRU 0625-FIM-PERFIL-MAC.
000000
000000    GO TO 0710-MONTA-MATRIZ-EXC-PERFIL.
000000
000000  0730-MONTA-PERFIL-FIM.
000000   EXIT.
000000
000000*---
000000
000000 10000-00-TRATA-ERRO-DMS.
000000*ERRO BANDO DE DADOS
000000     MOVE    DMSTATUS (DMRESULT) TO  DMS-RESULT-WS.
000000     MOVE FORM-KEY-IN      OF COMS-IN TO FORM-KEY-WS
000000     MOVE SDFINFO          OF COMS-IN TO SDFINFO-WS
000000     MOVE PROGRAMDESG      OF COMS-IN TO PROGRAMDESG-WS
000000     MOVE FUNCTIONINDEX    OF COMS-IN TO FUNCTIONINDEX-WS
000000     MOVE USERCODE         OF COMS-IN TO USERCODE-WS
000000     MOVE SECURITYDESG     OF COMS-IN TO SECURITYDESG-WS
000000     MOVE TIMESTAMP        OF COMS-IN TO TIMESTAMP-WS
000000     MOVE STATION          OF COMS-IN TO STATION-WS
000000     MOVE TEXTLENGTH       OF COMS-IN TO TEXTLENGTH-WS
000000     MOVE STATUSVALUE      OF COMS-IN TO STATUSVALUE-WS
000000     MOVE RESTART          OF COMS-IN TO RESTART-WS
000000     MOVE AGENDA           OF COMS-IN TO AGENDA-WS
000000
000000     CALL "TRATA_EXCECAO_DMS OF S0997/OBJ/LIBRARY/INSTALACAO"
000000          USING   DMS-PARAMETROS-WS
000000                  ARRAY-IN-WS
000000          GIVING  RESULTADO-77.
000000
000000     IF  DMS-ENCERRAR
000000         STOP    RUN.
000000
000000     IF  DMS-ATIVAR-DMTERMINATE
000000         CALL    SYSTEM  DMTERMINATE.
000000
000000     MOVE "Erro no acesso ao banco de dados."
000000       TO MENSAGEM OF SV-AREA-REL.
000000
000000     MOVE 1 TO RESULTADO-77.
000000
000000 10000-99-TRATA-ERRO-DMS.
000000     EXIT.
000000
000000 99900-00-ULTIMO-PARAGRAFO.
000000      CLOSE BDSEGURANCA.
000000      STOP RUN.
000000
000000 END-OF-JOB.
000000
000000