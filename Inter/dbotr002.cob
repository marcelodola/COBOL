000000*---------------- I N I C I O   D O   C O D I G O ---------------*
000000 IDENTIFICATION DIVISION.
000000 PROGRAM-ID. DBOTR002.
000000***************************************************************
000000* DBOTR002 Versao 1.0 por Euler Gandra - 15-07-2021
000000*
000000* Carrega no Aurora os dados das transacoes extraidas do
000000* VisionPlus. Usado tanto para a extracao full quanto para a do
000000* processo diario.
000000*
000000* TIPOCARGA - Determina o tipo da carga Full ou Diario
000000*       No processo diario é necessario tratar as transacoes
000000*       internacionais que sao geradas em dois registros
000000*    F - Insere todos os registros na base
000000*    D - Atualiza as transacoes postadas/criadas nas dirias
000000*    A - Atualiza a transacao internacional com os dados da memo
000000***************************************************************
000000*
000000 ENVIRONMENT DIVISION.
000000 INPUT-OUTPUT SECTION.
000000 FILE-CONTROL.
000000     SELECT ATZAMSL    ASSIGN         TO WS-ENTRATR
000000                       ORGANIZATION   IS RECORD SEQUENTIAL
000000                       FILE STATUS    IS WS-FS-ENTRADA.
000000*
000000 DATA DIVISION.
000000 FILE SECTION.
000000*
000000*****************************************************************
000000*
000000 FD  ATZAMSL
000000     RECORD CONTAINS 450 CHARACTERS
000000     BLOCK  CONTAINS 000 RECORDS
000000     RECORDING MODE IS F.
000000*
000000 01  REGISTRO.
000000*---------------------------------------------------------------*
000000*    H-TIPO-REG = '0'                                           *
000000*---------------------------------------------------------------*
000000*
000000     03 H-HEADER.
000000        06 H-TIPO-REG                    PIC  X(01).
000000        06 H-DATA-ARQ                    PIC  9(08).
000000        06 H-HORA-ARQ                    PIC  9(06).
000000        06 H-DATA-PROC                   PIC  9(08).
000000        06 FILLER                        PIC  X(427).
000000*
000000*---------------------------------------------------------------*
000000*    REGISTRO DE DETALHE - REGISTRO 2 - TRANSACOES              *
000000*---------------------------------------------------------------*
000000*
000000     03 TR-DETALHE REDEFINES H-HEADER.
000000        06 TR-TIPO-REG                   PIC  X(01).
000000        06 TR-BUSINESS-UNIT              PIC  X(03).
000000        06 TR-NUMERO-CONTA               PIC  X(19).
000000        06 TR-NUMERO-CARTAO              PIC  X(19).
000000        06 TR-CODIGO-AUTORIZACAO         PIC  X(06).
000000        06 TR-NUMERO-BANKNET             PIC  X(15).
000000        06 TR-CODIGO-TRANSACAO           PIC  X(05).
000000        06 TR-DATA-EFETIVACAO            PIC  X(08).
000000        06 TR-DATA-POSTAGEM              PIC  X(08).
000000        06 TR-IND-DDA                    PIC  X(01).
000000        06 TR-DIAS-EXTRATO               PIC  9(03).
000000        06 TR-CATEGORIA                  PIC  9(04).
000000        06 TR-MODO-ENTRADA               PIC  X(02).
000000        06 TR-MODULO-LOGICO              PIC  9(03).
000000        06 TR-DESC-ESTABELECIMENTO       PIC  X(40).
000000        06 TR-ID-ESTABELECIMENTO         PIC  X(21).
000000        06 ID-ESTAB REDEFINES TR-ID-ESTABELECIMENTO.
000000           07 WS-ID-ADQUIRENTE           PIC  9(06).
000000           07 WS-ID-ESTABELECIMENTO      PIC  X(15).
000000        06 TR-NUM-REGISTRO               PIC  9(05).
000000        06 TR-PLANO-CREDITO              PIC  9(05).
000000        06 TR-REFERENCIA                 PIC  X(23).
000000        06 TR-COD-REFERENCIA-ARN         PIC  X(23).
000000        06 TR-STATUS-ARQUIVO             PIC  X(02).
000000        06 TR-STATUS-TRANSACAO           PIC  X(01).
000000        06 TR-SITUACAO-TRANSACAO         PIC  X(01).
000000        06 TR-TIPO-TRANSACAO             PIC  X(01).
000000        06 TR-VAL-TOTAL-COMPRA           PIC  9(17).
000000        06 TR-VAL-TRANSACAO              PIC  9(17).
000000        06 TR-PARCELA-ATUAL              PIC  9(02).
000000        06 TR-TOTAL-PARCELAS             PIC  9(02).
000000        06 TR-MODO-ENT-AUTORIZACAO       PIC  X(02).
000000        06 TR-VALOR-TRANS-MOEDA-ORIG     PIC  9(17).
000000        06 TR-MOEDA-ORIGINAL             PIC  9(03).
000000        06 TR-COTACAO-AUTORIZACAO        PIC  9(06).
000000        06 TR-COTACAO-CLEARING           PIC  9(06).
000000        06 TR-VAL-TRANS-USD-AUTORIZACAO  PIC  9(17).
000000        06 TR-VAL-TRANS-USD-CLEARING     PIC  9(17).
000000        06 TR-HORA-TRANSACAO             PIC  9(06).
000000        06 TR-LOCALIDADE                 PIC  X(40).
000000        06 TR-DATA-VENCIMENTO            PIC  X(08).
000000        06 TR-IDENTIF-ORIGEM-VP          PIC  X(03).
000000        06 FILLER                        PIC  X(68).
000000     03 TR-AUX REDEFINES H-HEADER.
000000        06 REGISTRO-AUX                  PIC  X(450).
000000*
000000 WORKING-STORAGE SECTION.
000000 77  WS-DEBUG-CNT                PIC  9(04)          VALUE 0.
000000 77  WS-DEBUG                    PIC  9(01)          VALUE 0.
000000 77  WS-TRUNCA                   PIC  9(01)          VALUE 0.
000000 77  WS-TIPOCARGA                PIC  X(01)          VALUE SPACES.
000000 77  WS-I                        PIC S9(04) COMP     VALUE 0.
000000 77  WS-COMMITA                  PIC  9(05)          VALUE 1000.
000000 77  WS-FS-ENTRADA               PIC  9(02)          VALUE 0.
000000 77  WS-EOF                      PIC  X(01)          VALUE 'N'.
000000 77  WS-NUM-LIN                  PIC  9(09)          VALUE 0.
000000 77  WS-NUM-OK                   PIC  9(09)          VALUE 0.
000000 77  WS-NUM-UP                   PIC  9(09)          VALUE 0.
000000 77  WS-NUM-ERROS                PIC  9(09)          VALUE 0.
000000 77  WS-QUOCIENTE                PIC  9(09)          VALUE 0.
000000 77  WS-RESTO                    PIC  9(09)          VALUE 0.
000000 77  WS-NUM-OKN                  PIC  9(09)          VALUE 0.
000000*
000000     COPY AZD51WS.
000000*
000000 01  AREAS-AUXILIARES.
000000     03 FILLER                   PIC  X(20)          VALUE
000000                                           'INICIO WORK DBOTR002'.
000000*
000000     03 WS-REFERENCIA-AUX        PIC  X(23)          VALUE SPACES.
000000     03 WS-BANKNET-AUX           PIC  X(15)          VALUE SPACES.
000000     03 WS-ENV                                       VALUE SPACES.
000000        06 WS-ENV-NUM            PIC  X(01).
000000        06 FILLER                PIC  X(99).
000000*
000000     03 WS-TIME                  PIC  9(08)          VALUE 0.
000000     03 WS-TIME-R                REDEFINES WS-TIME.
000000        06 WS-HH                 PIC  9(02).
000000        06 WS-MM                 PIC  9(02).
000000        06 WS-SS                 PIC  9(02).
000000*
000000     03 WS-MSG-ERRO.
000000        06 WS-NOMEPRG            PIC  X(11) VALUE 'DBOTR002 - '.
000000        06 WS-OPERACAO           PIC  X(10)          VALUE SPACES.
000000        06 FILLER                PIC  X(01)          VALUE SPACES.
000000        06 WS-CHAVE              PIC  X(19)          VALUE SPACES.
000000        06 FILLER                PIC  X(01)          VALUE SPACES.
000000        06 WS-RESULT             PIC  X(06)          VALUE SPACES.
000000        06 FILLER                PIC  X(01)          VALUE SPACES.
000000        06 WS-SQLCODE-EDT        PIC -----9          VALUE 0.
000000        06 FILLER                PIC  X(01)          VALUE SPACES.
000000        06 WS-NUM-LINHA          PIC  9(08)          VALUE 0.
000000        06 FILLER                PIC  X(01)          VALUE SPACES.
000000        06 WS-SQLERRMC           PIC  X(70)          VALUE SPACES.
000000*
000000     03 WS-HEADER.
000000        06 WS-NOMEPRG            PIC  X(11) VALUE 'DBOTR002 - '.
000000        06 FILLER                PIC  X(12) VALUE 'PROCESSANDO'.
000000        06 WS-NOME-ARQ           PIC  X(11)          VALUE SPACES.
000000        06 FILLER                PIC  X(11) VALUE ' GERADO EM'.
000000        06 WS-DATA-ARQ           PIC  9(08)          VALUE 0.
000000        06 FILLER                PIC  X(01)          VALUE SPACES.
000000        06 WS-HORA-ARQ           PIC  9(08)          VALUE 0.
000000        06 FILLER                PIC  X(12) VALUE ' PROCESSADO '.
000000        06 WS-DATA-PROC          PIC  9(08)          VALUE 0.
000000*
000000     03 WS-TRAILER.
000000        06 WS-NOMEPRG            PIC  X(11) VALUE 'DBOTR002 - '.
000000        06 FILLER                PIC  X(12) VALUE 'FINALIZANDO'.
000000        06 WS-REGISTROS          PIC  9(08)          VALUE ZEROS.
000000        06 FILLER                PIC  X(20)          VALUE
000000                                           ' LINHAS PROCESSADAS.'.
000000        06 FILLER                PIC  X(01)          VALUE SPACES.
000000        06 WS-STATUS             PIC  X(03)          VALUE SPACES.
000000*
000000     03 WS-RESUMO.
000000        06 FILLER                PIC  X(12) VALUE 'INSERIDAS   '.
000000        06 WS-RESUMO-OK          PIC Z9(08)        VALUE 0.
000000        06 FILLER                PIC  X(11) VALUE ' TRANSACOES'.
000000*
000000     03 WS-RESUMO-UP.
000000        06 FILLER                PIC  X(12) VALUE 'ATUALIZADAS '.
000000        06 WS-RESUMO-OK-UP       PIC Z9(08)        VALUE 0.
000000        06 FILLER                PIC  X(11) VALUE ' TRANSACOES'.
000000*
000000     03 WS-RESUMO-ERR.
000000        06 FILLER                PIC  X(12) VALUE 'DESCARTADAS '.
000000        06 WS-RESUMO-OKN         PIC Z9(08)        VALUE 0.
000000        06 FILLER                PIC  X(11) VALUE ' TRANSACOES'.
000000*
000000*---------------------------------------------------------------*
000000*    Definicao de tabelas e areas na DCLGEN                     *
000000*---------------------------------------------------------------*
000000*
000000     EXEC SQL
000000          BEGIN DECLARE SECTION
000000     END-EXEC.
000000*
000000     EXEC SQL
000000          INCLUDE SQLCA
000000     END-EXEC.
000000*
000000*---------------------------------------------------------------*
000000*    TABELA TRANSACOES                                          *
000000*---------------------------------------------------------------*
000000*
000000 01  TRANSACOES.
000000     03 CODIGO-BUSINESS-UNIT          PIC  X(03).
000000     03 NUMERO-CONTA                  PIC  X(19).
000000     03 NUMERO-CARTAO                 PIC  X(19).
000000     03 NUMERO-BANKNET                PIC  X(23).
000000     03 CODIGO-AUTORIZACAO            PIC  X(06).
000000     03 CODIGO-TRANSACAO              PIC  X(05).
000000     03 DATA-EFETIVACAO               PIC  X(08).
000000     03 DATA-POSTAGEM                 PIC  X(08).
000000     03 IND-DDA                       PIC  X(01).
000000     03 DIAS-EXTRATO                  PIC  9(03).
000000     03 MCC                           PIC  9(04).
000000     03 MODO-ENTRADA                  PIC  X(02).
000000     03 MODULO-LOGICO                 PIC  9(03).
000000     03 ESTABELECIMENTO               PIC  X(40).
000000     03 ID-ESTABELECIMENTO            PIC  X(21).
000000     03 NUM-REGISTRO                  PIC  9(05).
000000     03 PLANO-CREDITO                 PIC  9(05).
000000     03 REFERENCIA                    PIC  X(23).
000000     03 COD-REFERENCIA-ARN            PIC  X(23).
000000     03 STATUS-ARQUIVO                PIC  X(02).
000000     03 STATUS-TRANSACAO              PIC  X(01).
000000     03 SITUACAO-TRANSACAO            PIC  X(01).
000000     03 TIPO-TRANSACAO                PIC  X(01).
000000     03 VALOR-TOTAL-COMPRA            PIC  9(17).
000000     03 VALOR-TRANSACAO               PIC  9(17).
000000     03 DATA-CRIACAO                  PIC  X(08).
000000     03 DATA-MODIFICACAO              PIC  X(08).
000000     03 PARCELA-ATUAL                 PIC  9(02).
000000     03 TOTAL-PARCELAS                PIC  9(02).
000000     03 IDENTIFICADOR-ORIGEM-VP       PIC  X(03).
000000     03 MODO-ENTRADA-AUTORIZACAO      PIC  X(02).
000000     03 VALOR-TRANSACAO-MOEDA-ORIG    PIC  9(17).
000000     03 VALOR-TRANS-DOLAR-AUTORIZACAO PIC  9(17).
000000     03 VALOR-TRANS-DOLAR-CLEARING    PIC  9(17).
000000     03 MOEDA-ORIGINAL                PIC  9(03).
000000     03 COTACAO-DOLAR-AUTORIZACAO     PIC  9(06).
000000     03 COTACAO-DOLAR-CLEARING        PIC  9(08).
000000     03 HORA-TRANSACAO                PIC  9(06).
000000     03 LOCALIDADE                    PIC  X(40).
000000     03 DATA-VENCIMENTO               PIC  X(08).
000000*
000000 01  WS-DATABASE                      PIC  X(20) VARYING.
000000 01  WS-PREP                          PIC  X(2000) VALUE SPACES.
000000 01  WS-PREP-UP                       PIC  X(2000) VALUE SPACES.
000000 01  WS-PREP-INS                      PIC  X(2000) VALUE SPACES.
000000*
000000     EXEC SQL
000000          END DECLARE SECTION
000000     END-EXEC.
000000*
000000 PROCEDURE DIVISION.
000000*
000000     PERFORM 0100-LE-PARAMETROS THRU 0100-LE-PARAMETROS-FIM.
000000*
000000     ACCEPT WS-TIME    FROM TIME.
000000     DISPLAY 'INICIANDO DBOTR002: ' WS-HH ':' WS-MM ':' WS-SS.
000000*
000000     MOVE 'N'          TO WS-EOF.
000000     OPEN INPUT   ATZAMSL.
000000     IF WS-FS-ENTRADA  NOT = ZEROS
000000        DISPLAY ' '
000000        DISPLAY 'Arquivo de dados nao foi aberto. Nao existe ou fa
000000-    'lta permissão?'
000000        DISPLAY ' '
000000        GO TO TERMINA-PROGRAMA
000000     END-IF.
000000*
000000***** Conecta no Aurora
000000*
000000     EXEC SQL
000000        CONNECT DSN :WS-DATABASE
000000     END-EXEC.
000000*
000000     IF SQLCODE    NOT = +0
000000        DISPLAY ' '
000000        DISPLAY 'Nao conectou no Aurora. Verifique parametros'
000000        DISPLAY 'SQLCODE = ' SQLCODE
000000        GO TO TERMINA-PROGRAMA
000000     END-IF.
000000*
000000     PERFORM UNTIL WS-EOF   = 'Y'
000000        READ ATZAMSL   AT END
000000             MOVE 'Y'  TO WS-EOF
000000                   NOT AT END
000000             PERFORM 0200-PROCESSA
000000                THRU 0200-PROCESSA-FIM  
000000        END-READ
000000     END-PERFORM.
000000*
000000     PERFORM 0500-TRATA-TRAILER
000000        THRU 0500-TRATA-TRAILER-FIM.      
000000*
000000 0100-LE-PARAMETROS.
000000     MOVE SPACES       TO WS-DATABASE-ARR.
000000     MOVE SPACES       TO WS-ENTRATR.
000000     MOVE SPACES       TO WS-TIPOCARGA.
000000     MOVE ZEROS        TO WS-DEBUG.
000000     MOVE ZEROS        TO WS-TRUNCA.
000000*
000000     DISPLAY 'ENTRATR'      UPON ENVIRONMENT-NAME.
000000     ACCEPT  WS-ENV         FROM ENVIRONMENT-VALUE
000000             ON EXCEPTION
000000                MOVE SPACES TO WS-ENTRATR
000000         NOT ON EXCEPTION
000000                MOVE WS-ENV TO WS-ENTRATR
000000     END-ACCEPT.
000000*
000000     DISPLAY 'TIPOCARGA'    UPON ENVIRONMENT-NAME.
000000     ACCEPT  WS-ENV         FROM ENVIRONMENT-VALUE
000000             ON EXCEPTION
000000                MOVE 'F'    TO WS-TIPOCARGA
000000         NOT ON EXCEPTION
000000                MOVE WS-ENV TO WS-TIPOCARGA
000000     END-ACCEPT.
000000*
000000     DISPLAY 'DEBUG'        UPON ENVIRONMENT-NAME.
000000     ACCEPT  WS-ENV         FROM ENVIRONMENT-VALUE
000000             ON EXCEPTION
000000                MOVE ZEROS  TO WS-DEBUG
000000         NOT ON EXCEPTION
000000                IF WS-ENV-NUM    NOT NUMERIC OR
000000                   WS-ENV-NUM    = '0'
000000                   MOVE ZEROS    TO WS-DEBUG
000000                ELSE
000000                   MOVE 1        TO WS-DEBUG
000000                END-IF
000000     END-ACCEPT.
000000*
000000     DISPLAY 'TRUNCA'       UPON ENVIRONMENT-NAME.
000000     ACCEPT  WS-ENV         FROM ENVIRONMENT-VALUE
000000             ON EXCEPTION
000000                MOVE ZEROS  TO WS-TRUNCA
000000        NOT ON EXCEPTION
000000              IF WS-ENV-NUM      NOT NUMERIC OR
000000                 WS-ENV-NUM      = '0'
000000                 MOVE ZEROS      TO WS-TRUNCA
000000              ELSE
000000                 MOVE 1          TO WS-TRUNCA
000000             END-IF
000000     END-ACCEPT.
000000*
000000     DISPLAY 'DATABASE'     UPON ENVIRONMENT-NAME.
000000     ACCEPT  WS-ENV         FROM ENVIRONMENT-VALUE
000000             ON EXCEPTION
000000                MOVE SPACES TO WS-DATABASE-ARR
000000         NOT ON EXCEPTION
000000                MOVE WS-ENV TO WS-DATABASE-ARR
000000     END-ACCEPT.
000000     PERFORM VARYING  WS-I  FROM 1    BY 1
000000             UNTIL WS-DATABASE-ARR(WS-I:1)      = ' '
000000     END-PERFORM.
000000     SUBTRACT 1        FROM WS-I.
000000     MOVE WS-I         TO WS-DATABASE-LEN.
000000*
000000     IF WS-ENTRATR         = SPACES     OR
000000        WS-ENTRATR         = LOW-VALUES OR
000000        WS-DATABASE        = SPACES     OR
000000        WS-DATABASE        = LOW-VALUES OR
000000        WS-TIPOCARGA       = SPACES     OR
000000        WS-TIPOCARGA       = LOW-VALUES
000000        DISPLAY 'FALTAM PARAMETROS. SAO ESPERADOS TRES VARIAVEIS D
000000-               'E AMBIENTE'
000000*
000000        STOP RUN
000000     ELSE
000000       DISPLAY 'PARAMETROS LIDOS - DEBUG:'WS-DEBUG' TRUNCA:'
000000-        WS-TRUNCA ' CARGA:'WS-TIPOCARGA ' ARQUIVO:'WS-ENTRATR
000000     END-IF.
000000*
000000 0100-LE-PARAMETROS-FIM. EXIT.
000000*
000000 0200-PROCESSA.
000000     ADD 1             TO WS-NUM-LIN.
000000     DIVIDE WS-NUM-LIN BY WS-COMMITA  GIVING    WS-QUOCIENTE
000000                                      REMAINDER WS-RESTO.
000000     IF WS-RESTO       = 0
000000        IF WS-DEBUG = 0 OR WS-TRUNCA = 1
000000           EXEC SQL
000000                COMMIT
000000           END-EXEC
000000*
000000           MOVE 'COMMIT CON'   TO WS-OPERACAO
000000           PERFORM R888-TRATA-SQLCODE
000000        END-IF
000000     END-IF.
000000*
000000     MOVE LENGTH OF REGISTRO-AUX TO AZ51WS-AREA-LENGTH.
000000     MOVE REGISTRO-AUX           TO AZ51WS-AREA-INSPECT.
000000     PERFORM I000-CORRECT-CHARACTER THRU I000-CORRCH-EXIT.
000000*
000000     IF AZ51WS-ALTERADO-S
000000        MOVE AZ51WS-CHAR-ALT-CHAR  TO REGISTRO-AUX
000000     END-IF.
000000*
000000     EVALUATE H-TIPO-REG
000000         WHEN '0'
000000              PERFORM 0300-TRATA-HEADER
000000                 THRU 0300-TRATA-HEADER-FIM
000000         WHEN '1'
000000              CONTINUE
000000         WHEN '2'
000000              PERFORM 0400-TRATA-TRANSACAO 
000000                 THRU 0400-TRATA-TRANSACAO-FIM
000000         WHEN OTHER
000000              PERFORM 0700-TRATA-ERRO
000000                 THRU 0700-TRATA-ERRO-FIM                                   
000000     END-EVALUATE.
000000*
000000 0200-PROCESSA-FIM. EXIT. 
000000*
000000 0300-TRATA-HEADER.
000000*
000000     MOVE H-DATA-ARQ        TO WS-DATA-ARQ.
000000     MOVE H-HORA-ARQ        TO WS-HORA-ARQ.
000000     MOVE H-DATA-PROC       TO WS-DATA-PROC.
000000     DISPLAY WS-HEADER.
000000*
000000     IF WS-TRUNCA      = 1 AND WS-DEBUG = 0
000000        EXEC SQL
000000             TRUNCATE transacoesvision.t_transacoes
000000        END-EXEC
000000        MOVE 'TRNC TRANS'          TO WS-OPERACAO
000000        PERFORM R888-TRATA-SQLCODE
000000     END-IF.
000000*
000000 0300-TRATA-HEADER-FIM. EXIT.
000000*
000000 0400-TRATA-TRANSACAO.
000000*
000000     ADD 1                        TO WS-REGISTROS.
000000*
000000     INITIALIZE TRANSACOES
000000      REPLACING NUMERIC DATA     BY ZERO
000000           ALPHANUMERIC DATA     BY SPACES.
000000*
000000     IF TR-BUSINESS-UNIT = SPACES
000000        MOVE '000'                TO CODIGO-BUSINESS-UNIT
000000     ELSE
000000        MOVE TR-BUSINESS-UNIT     TO CODIGO-BUSINESS-UNIT
000000     END-IF.
000000*
000000     MOVE TR-NUMERO-CONTA         TO NUMERO-CONTA.
000000     MOVE TR-NUMERO-CARTAO        TO NUMERO-CARTAO.
000000     MOVE TR-CODIGO-AUTORIZACAO   TO CODIGO-AUTORIZACAO.
000000*
000000     IF TR-CODIGO-TRANSACAO = SPACES
000000        MOVE '00000'              TO CODIGO-TRANSACAO
000000     ELSE
000000        MOVE TR-CODIGO-TRANSACAO  TO CODIGO-TRANSACAO
000000     END-IF.
000000*
000000*    Quando for tipo carga A seleciona somente as memos das
000000*    transacoes internacionais para atualizar
000000*
000000     IF TR-CODIGO-TRANSACAO NOT = '00096' AND
000000        WS-TIPOCARGA = 'A'
000000        GO TO 0400-TRATA-TRANSACAO-FIM
000000     END-IF.
000000*
000000     MOVE TR-IND-DDA              TO IND-DDA.
000000*
000000     IF TR-CATEGORIA NOT NUMERIC
000000        MOVE ZEROS                TO MCC
000000     ELSE
000000        MOVE TR-CATEGORIA         TO MCC
000000     END-IF.
000000*
000000     IF MCC = ZEROS
000000         MOVE CODIGO-TRANSACAO    TO MCC
000000     END-IF.
000000*
000000     IF TR-DIAS-EXTRATO NOT NUMERIC
000000        MOVE ZEROS                TO DIAS-EXTRATO
000000     ELSE
000000        MOVE TR-DIAS-EXTRATO      TO DIAS-EXTRATO
000000     END-IF.
000000*
000000     MOVE TR-MODO-ENTRADA         TO MODO-ENTRADA.
000000*
000000     IF TR-MODULO-LOGICO NOT NUMERIC
000000        MOVE ZEROS                TO MODULO-LOGICO
000000     ELSE
000000        MOVE TR-MODULO-LOGICO     TO MODULO-LOGICO
000000     END-IF.
000000*
000000     MOVE TR-DESC-ESTABELECIMENTO TO ESTABELECIMENTO.
000000*
000000     IF TR-ID-ESTABELECIMENTO = SPACES
000000        MOVE ZEROS                TO ID-ESTABELECIMENTO
000000     ELSE
000000        MOVE TR-ID-ESTABELECIMENTO TO ID-ESTABELECIMENTO
000000     END-IF.
000000*
000000     IF WS-ID-ADQUIRENTE = ZEROS AND
000000        WS-ID-ESTABELECIMENTO = SPACES
000000        STRING TR-BUSINESS-UNIT CODIGO-TRANSACAO
000000        DELIMITED BY SIZE INTO ID-ESTABELECIMENTO
000000     END-IF.
000000*
000000     IF TR-PARCELA-ATUAL NOT NUMERIC
000000        MOVE ZEROS                TO PARCELA-ATUAL
000000     ELSE
000000        MOVE TR-PARCELA-ATUAL     TO PARCELA-ATUAL
000000     END-IF.
000000*
000000     IF TR-TOTAL-PARCELAS NOT NUMERIC
000000        MOVE ZEROS                TO TOTAL-PARCELAS
000000     ELSE
000000        MOVE TR-TOTAL-PARCELAS    TO TOTAL-PARCELAS
000000     END-IF.
000000*
000000     IF TR-NUM-REGISTRO NOT NUMERIC
000000        MOVE ZEROS                TO NUM-REGISTRO
000000     ELSE
000000        MOVE TR-NUM-REGISTRO      TO NUM-REGISTRO
000000     END-IF.
000000*
000000     IF TR-PLANO-CREDITO NOT NUMERIC
000000        MOVE ZEROS                TO PLANO-CREDITO
000000     ELSE
000000        MOVE TR-PLANO-CREDITO     TO PLANO-CREDITO
000000     END-IF.
000000*
000000     MOVE TR-COD-REFERENCIA-ARN   TO COD-REFERENCIA-ARN.
000000     MOVE TR-STATUS-ARQUIVO       TO STATUS-ARQUIVO.
000000     MOVE TR-STATUS-TRANSACAO     TO STATUS-TRANSACAO.
000000     MOVE TR-SITUACAO-TRANSACAO   TO SITUACAO-TRANSACAO.
000000     MOVE TR-TIPO-TRANSACAO       TO TIPO-TRANSACAO.
000000*
000000     MOVE TR-REFERENCIA           TO WS-REFERENCIA-AUX.
000000     INSPECT WS-REFERENCIA-AUX REPLACING ALL "0" BY SPACE.
000000*
000000     IF WS-REFERENCIA-AUX = SPACES
000000        MOVE SPACES                TO REFERENCIA
000000     ELSE
000000        MOVE TR-REFERENCIA         TO REFERENCIA
000000     END-IF.
000000*
000000     MOVE TR-NUMERO-BANKNET       TO WS-BANKNET-AUX.
000000     INSPECT WS-BANKNET-AUX REPLACING ALL "0" BY SPACE.
000000*
000000     IF WS-BANKNET-AUX = SPACES
000000        MOVE SPACES               TO NUMERO-BANKNET
000000        IF REFERENCIA NOT = SPACES
000000           MOVE REFERENCIA        TO NUMERO-BANKNET 
000000        END-IF
000000     ELSE
000000        MOVE TR-NUMERO-BANKNET    TO NUMERO-BANKNET
000000     END-IF.
000000*
000000     IF TR-VAL-TOTAL-COMPRA NOT NUMERIC
000000        MOVE ZEROS                TO VALOR-TOTAL-COMPRA
000000     ELSE
000000        MOVE TR-VAL-TOTAL-COMPRA  TO VALOR-TOTAL-COMPRA
000000     END-IF.
000000*
000000     IF TR-VAL-TRANSACAO NOT NUMERIC
000000        MOVE ZEROS                TO VALOR-TRANSACAO
000000     ELSE
000000        MOVE TR-VAL-TRANSACAO     TO VALOR-TRANSACAO
000000     END-IF.
000000*
000000     IF TR-DATA-EFETIVACAO   = SPACES
000000        MOVE '00000000'           TO DATA-EFETIVACAO
000000     ELSE
000000        MOVE TR-DATA-EFETIVACAO   TO DATA-EFETIVACAO
000000     END-IF.
000000*
000000     IF TR-DATA-POSTAGEM     = SPACES
000000        MOVE '00000000'           TO DATA-POSTAGEM
000000     ELSE
000000        MOVE TR-DATA-POSTAGEM     TO DATA-POSTAGEM
000000     END-IF.
000000*
000000     IF TR-DATA-VENCIMENTO   = SPACES
000000        MOVE '00000000'           TO DATA-VENCIMENTO
000000     ELSE
000000        MOVE TR-DATA-VENCIMENTO   TO DATA-VENCIMENTO
000000     END-IF.
000000*
000000     MOVE TR-IDENTIF-ORIGEM-VP    TO IDENTIFICADOR-ORIGEM-VP.
000000     MOVE TR-MODO-ENT-AUTORIZACAO TO MODO-ENTRADA-AUTORIZACAO.
000000     MOVE TR-LOCALIDADE           TO LOCALIDADE.
000000*
000000     IF TR-HORA-TRANSACAO NOT NUMERIC
000000        MOVE ZEROS                TO HORA-TRANSACAO
000000     ELSE
000000        MOVE TR-HORA-TRANSACAO    TO HORA-TRANSACAO
000000     END-IF.
000000*
000000     IF TR-COTACAO-CLEARING NOT NUMERIC
000000        MOVE ZEROS                TO COTACAO-DOLAR-CLEARING
000000     ELSE
000000        MOVE TR-COTACAO-CLEARING  TO COTACAO-DOLAR-CLEARING
000000     END-IF.
000000*
000000     IF TR-COTACAO-AUTORIZACAO NOT NUMERIC
000000        MOVE ZEROS                TO COTACAO-DOLAR-AUTORIZACAO
000000     ELSE
000000        MOVE TR-COTACAO-AUTORIZACAO  TO COTACAO-DOLAR-AUTORIZACAO
000000     END-IF.
000000*
000000     IF TR-MOEDA-ORIGINAL NOT NUMERIC
000000        MOVE ZEROS                TO MOEDA-ORIGINAL
000000     ELSE
000000        MOVE TR-MOEDA-ORIGINAL    TO MOEDA-ORIGINAL
000000     END-IF.
000000*
000000     IF TR-VALOR-TRANS-MOEDA-ORIG NOT NUMERIC
000000        MOVE ZEROS                TO VALOR-TRANSACAO-MOEDA-ORIG
000000     ELSE
000000        MOVE TR-VALOR-TRANS-MOEDA-ORIG  TO
000000                                     VALOR-TRANSACAO-MOEDA-ORIG
000000     END-IF.
000000*
000000     IF TR-VAL-TRANS-USD-AUTORIZACAO NOT NUMERIC
000000        MOVE ZEROS               TO VALOR-TRANS-DOLAR-AUTORIZACAO
000000     ELSE
000000        MOVE TR-VAL-TRANS-USD-AUTORIZACAO  TO
000000                                    VALOR-TRANS-DOLAR-AUTORIZACAO
000000     END-IF.
000000*
000000     IF TR-VAL-TRANS-USD-CLEARING NOT NUMERIC
000000        MOVE ZEROS                  TO VALOR-TRANS-DOLAR-CLEARING
000000     ELSE
000000        MOVE TR-VAL-TRANS-USD-CLEARING  TO
000000                                     VALOR-TRANS-DOLAR-CLEARING
000000     END-IF.
000000*
000000     PERFORM 0600-MONTA-SQL
000000        THRU 0600-MONTA-SQL-FIM.
000000*
000000     IF WS-DEBUG = 1
000000       ADD 1               TO WS-DEBUG-CNT
000000       IF WS-DEBUG-CNT > 1000
000000          GO TO TERMINA-PROGRAMA
000000       END-IF
000000       DISPLAY WS-DEBUG-CNT ':' WS-PREP
000000     ELSE
000000        EXEC SQL
000000            EXECUTE IMMEDIATE :WS-PREP
000000        END-EXEC
000000     END-IF.
000000*
000000     PERFORM R888-TRATA-SQLCODE.
000000*
000000     IF SQLCODE    NOT = +0 AND +100
000000        ADD 1          TO WS-NUM-OKN
000000     ELSE
000000        IF WS-OPERACAO = 'INS TRANS.'
000000           ADD 1       TO WS-NUM-OK
000000        ELSE
000000           ADD 1       TO WS-NUM-UP
000000        END-IF
000000     END-IF.
000000*
000000 0400-TRATA-TRANSACAO-FIM.
000000     EXIT.
000000*
000000 0500-TRATA-TRAILER.
000000*
000000     MOVE 'OK '        TO WS-STATUS.
000000*
000000     IF WS-NUM-OKN GREATER 0
000000        MOVE 'NOK'     TO WS-STATUS
000000     END-IF.
000000*
000000     DISPLAY WS-TRAILER.
000000*
000000     MOVE WS-NUM-OK    TO WS-RESUMO-OK.
000000     DISPLAY WS-RESUMO.
000000*
000000     MOVE WS-NUM-UP    TO WS-RESUMO-OK-UP.
000000     DISPLAY WS-RESUMO-UP.
000000*
000000     MOVE WS-NUM-OKN   TO WS-RESUMO-OKN.
000000     DISPLAY WS-RESUMO-ERR.
000000*
000000     IF WS-DEBUG = 0 OR WS-TRUNCA = 1
000000        EXEC SQL
000000             COMMIT
000000        END-EXEC
000000     END-IF.
000000*
000000     GO TO TERMINA-PROGRAMA.
000000*
000000 0500-TRATA-TRAILER-FIM. EXIT. 
000000*
000000 R888-TRATA-SQLCODE.
000000*
000000     IF SQLCODE    NOT = +0 AND +100
000000        MOVE 'ERR   '       TO WS-RESULT
000000        MOVE SQLCODE        TO WS-SQLCODE-EDT
000000*
000000        IF (SQLERRML > ZERO) AND (SQLERRML < 80)
000000            MOVE SQLERRMC(1:SQLERRML) TO WS-SQLERRMC
000000        ELSE
000000            MOVE SQLERRMC   TO WS-SQLERRMC
000000        END-IF
000000*
000000        MOVE WS-NUM-LIN     TO WS-NUM-LINHA
000000        MOVE TR-NUMERO-CONTA TO WS-CHAVE
000000        DISPLAY WS-MSG-ERRO
000000        DISPLAY 'SQL ERRO:'WS-PREP
000000        GO TO TERMINA-PROGRAMA
000000     ELSE
000000        IF SQLCODE = 100
000000           EXEC SQL
000000               EXECUTE IMMEDIATE :WS-PREP-INS
000000           END-EXEC
000000*
000000           DISPLAY 'SQL INS:'WS-PREP-INS
000000           MOVE 'INS TRANS.' TO WS-OPERACAO
000000           PERFORM R888-TRATA-SQLCODE
000000              THRU R888-TRATA-SQLCODE-FIM                            
000000        END-IF
000000     END-IF.
000000*
000000 R888-TRATA-SQLCODE-FIM. EXIT.
000000*
000000 0600-MONTA-SQL.
000000*
000000     MOVE SPACES            TO WS-PREP.
000000     MOVE SPACES            TO WS-PREP-UP.
000000     MOVE SPACES            TO WS-PREP-INS.
000000*
000000     STRING 'INSERT INTO transacoesvision.t_transacoes'
000000-    ' (codigo_business_unit, numero_cartao, numero_conta,'
000000-    ' codigo_autorizacao, codigo_transacao, data_efetivacao,'
000000-    ' data_postagem, ind_dda, dias_extrato, mcc, modo_entrada,'
000000-    ' modulo_logico, estabelecimento, id_estabelecimento,'
000000-    ' num_registro, plano_credito, referencia,'
000000-    ' cod_referencia_arn, status_arquivo, status_transacao,'
000000-    ' tipo_transacao, situacao_transacao, valor_total_compra,'
000000-    ' valor_transacao, data_modificacao, data_criacao,'
000000-    ' parcela_atual, total_parcelas, numero_banknet,'
000000-    ' identificador_origem_vp, modo_entrada_autorizacao,'
000000-    ' valor_transacao_moeda_original, moeda_original,'
000000-    ' cotacao_dolar_autorizacao, cotacao_dolar_clearing,'
000000-    ' horatransacao, localidade, data_vencimento,'
000000-    ' valor_transacao_dolar_autorizacao,'
000000-    ' valor_transacao_dolar_clearing) VALUES ('
000000        DELIMITED BY SIZE
000000     ' trim('''CODIGO-BUSINESS-UNIT''')' DELIMITED BY SIZE
000000     ', trim('''NUMERO-CARTAO''')' DELIMITED BY SIZE
000000     ', trim('''NUMERO-CONTA''')' DELIMITED BY SIZE
000000     ', trim('''CODIGO-AUTORIZACAO''')' DELIMITED BY SIZE
000000     ', trim('''CODIGO-TRANSACAO''')' DELIMITED BY SIZE
000000     ', TO_DATE('''DATA-EFETIVACAO''',''YYYYMMDD'')'
000000        DELIMITED BY SIZE
000000     ', TO_DATE('''DATA-POSTAGEM''',''YYYYMMDD'')'
000000         DELIMITED BY SIZE
000000     ', trim('''IND-DDA''')' DELIMITED BY SIZE
000000     ', 'DIAS-EXTRATO DELIMITED BY SIZE
000000     ', 'MCC DELIMITED BY SIZE
000000     ', trim('''MODO-ENTRADA''')' DELIMITED BY SIZE
000000     ', 'MODULO-LOGICO DELIMITED BY SIZE
000000     ', trim('''ESTABELECIMENTO''')' DELIMITED BY SIZE
000000     ', trim('''ID-ESTABELECIMENTO''')' DELIMITED BY SIZE
000000     ', 'NUM-REGISTRO DELIMITED BY SIZE
000000     ', 'PLANO-CREDITO DELIMITED BY SIZE
000000     ', trim('''REFERENCIA''')' DELIMITED BY SIZE
000000     ', trim('''COD-REFERENCIA-ARN''')' DELIMITED BY SIZE
000000     ', trim('''STATUS-ARQUIVO''')' DELIMITED BY SIZE
000000     ', trim('''STATUS-TRANSACAO''')' DELIMITED BY SIZE
000000     ', trim('''TIPO-TRANSACAO''')' DELIMITED BY SIZE
000000     ', trim('''SITUACAO-TRANSACAO''')' DELIMITED BY SIZE
000000     ', 'VALOR-TOTAL-COMPRA DELIMITED BY SIZE
000000     ', 'VALOR-TRANSACAO DELIMITED BY SIZE
000000      ', CURRENT_DATE, CURRENT_DATE' DELIMITED BY SIZE
000000     ', 'PARCELA-ATUAL','TOTAL-PARCELAS
000000     ', trim('''NUMERO-BANKNET''')' DELIMITED BY SIZE
000000     ', trim('''IDENTIFICADOR-ORIGEM-VP''')' DELIMITED BY SIZE
000000     ', trim('''MODO-ENTRADA-AUTORIZACAO''')' DELIMITED BY SIZE
000000     ', 'VALOR-TRANSACAO-MOEDA-ORIG DELIMITED BY SIZE
000000     ', 'MOEDA-ORIGINAL DELIMITED BY SIZE
000000     ', 'COTACAO-DOLAR-AUTORIZACAO DELIMITED BY SIZE
000000     ', 'COTACAO-DOLAR-CLEARING DELIMITED BY SIZE
000000     ', 'HORA-TRANSACAO DELIMITED BY SIZE
000000     ', trim('''LOCALIDADE''')' DELIMITED BY SIZE
000000     ', TO_DATE('''DATA-VENCIMENTO''',''YYYYMMDD'')'
000000     ', 'VALOR-TRANS-DOLAR-AUTORIZACAO DELIMITED BY SIZE
000000     ', 'VALOR-TRANS-DOLAR-CLEARING');' DELIMITED BY SIZE
000000        INTO WS-PREP-INS
000000*
000000     STRING 'UPDATE transacoesvision.t_transacoes'
000000     ' SET situacao_transacao='''SITUACAO-TRANSACAO''''
000000     ', cotacao_dolar_clearing='COTACAO-DOLAR-CLEARING
000000     ', valor_transacao_moeda_original='VALOR-TRANSACAO-MOEDA-ORIG
000000     ', moeda_original='MOEDA-ORIGINAL
000000     ', cotacao_dolar_autorizacao='COTACAO-DOLAR-AUTORIZACAO
000000     ', status_arquivo=('''STATUS-ARQUIVO''')'
000000     ', status_transacao=('''STATUS-TRANSACAO''')'
000000     ', data_postagem=TO_DATE('''DATA-POSTAGEM''',''YYYYMMDD'')'
000000     ', data_modificacao=CURRENT_DATE'
000000     ', ind_dda=('''IND-DDA''')' DELIMITED BY SIZE
000000     ', modo_entrada=('''MODO-ENTRADA''')' DELIMITED BY SIZE
000000     ', referencia=('''REFERENCIA''')' DELIMITED BY SIZE
000000     ', cod_referencia_arn='
000000     '('''COD-REFERENCIA-ARN''')' DELIMITED BY SIZE
000000     ', dias_extrato='DIAS-EXTRATO DELIMITED BY SIZE
000000     ', parcela_atual='PARCELA-ATUAL DELIMITED BY SIZE
000000     ', num_registro=(case when num_registro = 0
000000     ' then 'NUM-REGISTRO' else num_registro end)'
000000       DELIMITED BY SIZE
000000     ', valor_transacao_dolar_clearing='VALOR-TRANS-DOLAR-CLEARING
000000       DELIMITED BY SIZE
000000     ', codigo_transacao=(case when codigo_transacao = '''''
000000     ' or codigo_transacao is null then '
000000     ' ('''CODIGO-TRANSACAO''') else codigo_transacao end)'
000000       DELIMITED BY SIZE
000000     ' WHERE ' DELIMITED BY SIZE
000000     ' codigo_business_unit=('''CODIGO-BUSINESS-UNIT''')'
000000     ' and numero_conta=('''NUMERO-CONTA''')'
000000     ' and numero_cartao=('''NUMERO-CARTAO''')'
000000     ' and codigo_autorizacao=('''CODIGO-AUTORIZACAO''')'
000000     ' and (data_vencimento is null or data_vencimento='
000000     'TO_DATE('''DATA-VENCIMENTO''',''YYYYMMDD''))@'
000000       DELIMITED BY SIZE INTO WS-PREP-UP
000000*
000000     IF NUMERO-BANKNET NOT = SPACES
000000        STRING WS-PREP-UP DELIMITED BY '@'
000000        ' and numero_banknet=trim('''NUMERO-BANKNET''')&'
000000        DELIMITED BY SIZE INTO WS-PREP-UP
000000        INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000        INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000     END-IF.
000000*
000000     IF CODIGO-TRANSACAO = '00096' AND WS-TIPOCARGA = 'A'
000000        STRING WS-PREP-UP DELIMITED BY '@'
000000        ' and codigo_transacao!= ''00096''&'
000000        DELIMITED BY SIZE INTO WS-PREP-UP
000000        INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000        INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000*
000000* SOMENTE FILTRA PELA REFERENCIA APOS O POSTING
000000*
000000        IF REFERENCIA NOT = SPACES
000000           STRING WS-PREP-UP DELIMITED BY '@'
000000           ' and referencia=trim('''REFERENCIA''')&'
000000           DELIMITED BY SIZE INTO WS-PREP-UP
000000           INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000           INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000        END-IF
000000     END-IF.
000000*
000000* SOMENTE FILTRA PELA DATA DE POSTAGEM PARA FAT. E POSTING.
000000*
000000     IF WS-TIPOCARGA = 'F' OR WS-TIPOCARGA = 'A'
000000        STRING WS-PREP-UP DELIMITED BY '@'
000000        ' and data_postagem='
000000-       'TO_DATE('''DATA-POSTAGEM''',''YYYYMMDD'')&'
000000        DELIMITED BY SIZE INTO WS-PREP-UP
000000        INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000        INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000     ELSE
000000        STRING WS-PREP-UP DELIMITED BY '@'
000000        ' and (data_postagem is null or'
000000-       ' data_postagem=TO_DATE(''00000000'',''YYYYMMDD''))&'
000000        DELIMITED BY SIZE INTO WS-PREP-UP
000000        INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000        INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000     END-IF.
000000*
000000     IF CODIGO-AUTORIZACAO = SPACES OR
000000        CODIGO-AUTORIZACAO = '000000' OR STATUS-ARQUIVO = 'OA'
000000         STRING WS-PREP-UP DELIMITED BY '@'
000000         ' and valor_transacao='VALOR-TRANSACAO
000000         DELIMITED BY SIZE
000000         ' and data_efetivacao='
000000     'TO_DATE('''DATA-EFETIVACAO''',''YYYYMMDD'')&'
000000         DELIMITED BY SIZE INTO WS-PREP-UP
000000         INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000         INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000*
000000         IF HORA-TRANSACAO NOT = ZEROS AND STATUS-ARQUIVO = 'OA'
000000             STRING WS-PREP-UP DELIMITED BY '@'
000000             ' and horatransacao='HORA-TRANSACAO'&'
000000             DELIMITED BY SIZE INTO WS-PREP-UP
000000             INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000             INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000         END-IF
000000     END-IF.
000000*
000000*    IF SITUACAO-TRANSACAO = 'O' OR WS-TIPOCARGA = 'F'
000000*        STRING WS-PREP-UP DELIMITED BY '@'
000000*        ' and valor_transacao='VALOR-TRANSACAO'&'
000000*        DELIMITED BY SIZE INTO WS-PREP-UP
000000*        INSPECT WS-PREP-UP REPLACING ALL '@' BY SPACE
000000*        INSPECT WS-PREP-UP REPLACING ALL '&' BY '@'
000000*    END-IF.
000000*
000000     INSPECT WS-PREP-UP REPLACING ALL '@' BY ';'.
000000     INSPECT WS-PREP-UP REPLACING ALL '&' BY SPACE.
000000*
000000     IF WS-TIPOCARGA = 'F'
000000        MOVE WS-PREP-INS    TO WS-PREP
000000        MOVE 'INS TRANS.'   TO WS-OPERACAO
000000     ELSE
000000        MOVE 'UPD TRANS.'   TO WS-OPERACAO
000000        MOVE WS-PREP-UP     TO WS-PREP
000000     END-IF.
000000*
000000 0600-MONTA-SQL-FIM. EXIT.
000000*
000000 0700-TRATA-ERRO.
000000     DISPLAY 'LINHA COM TIPO DE REGISTRO INVALIDO - ' WS-NUM-LIN.
000000 0700-TRATA-ERRO-FIM. EXIT. 
000000*
000000 TERMINA-PROGRAMA.
000000*
000000     CLOSE ATZAMSL.
000000     DISPLAY 'DBOTR002 - INICIO  : ' WS-HH ':' WS-MM ':' WS-SS.
000000*
000000     ACCEPT WS-TIME    FROM TIME.
000000     DISPLAY 'DBOTR002 - TERMINO : ' WS-HH ':' WS-MM ':' WS-SS.
000000*
000000     STOP RUN.
000000*
000000     COPY AZD51PD.
000000*
000000*----------------- F I M   D O   C O D I G O --------------------*
